#defaultView:Table
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?count ?wikiproject ?wikiprojectLabel (CONCAT("/wikiproject/", SUBSTR(STR(?wikiproject),32)) AS ?wikiprojectUrl) ?example_entity ?example_entityLabel (CONCAT("/", SUBSTR(STR(?example_entity),32)) AS ?example_entityUrl) WHERE {
  # Label the results
  {
    SELECT (COUNT(?work) AS ?count) ?wikiproject (SAMPLE(?work) AS ?example_entity) WHERE {
      # Find works for the specific queried wikiproject
      VALUES ?p { wdt:P6104 wdt:P5008 }
      ?work ?p target: .


      ### non-functional bd:sample call, may need to be reimplemented
      ### if searching through the entire db is too costly
      ### if implemented, also implement in template: "wikiproject_topics.sparql"      
      # SERVICE bd:sample {
      #   ?work ?p target: .
      #   bd:serviceParam bd:sample.limit 100000
      # }

      # Find co-occuring wikiprojects
      ?work ?p ?wikiproject .
      # Avoid listing the queried wikiproject
      FILTER (target: != ?wikiproject)
    }
    GROUP BY ?wikiproject
  }
  # SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?count)
