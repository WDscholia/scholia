#defaultView:Graph
SELECT
  ?author1 ?author1Label
  ?author2 ?author2Label
WITH {  
  SELECT
    DISTINCT 
    ?author1
    ?author2
  WHERE {
    ?author1 ( wdt:P108 | wdt:P1416 ) / wdt:P17 wd:{{q1}} .
    ?author2 ( wdt:P108 | wdt:P1416 ) / wdt:P17 wd:{{q1}} .
    ?work wdt:P921 / (wdt:P361+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) ) wd:{{q2}} .
    ?work wdt:P50 ?author1 .  
    ?work wdt:P50 ?author2 .
    FILTER (?author1 != ?author2)
  }
} AS %results
WHERE {
  INCLUDE %results
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,jp,nl,no,ru,sv,zh". }
}