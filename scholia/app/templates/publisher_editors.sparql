SELECT
  ?number_of_editorships
  ?editor ?editorLabel 
  ?example_venue ?example_venueLabel
WITH {
  SELECT
    (COUNT(?venue_) AS ?number_of_editorships)
    ?editor
    (SAMPLE(?venue_) AS ?example_venue)
    # (GROUP_CONCAT(?journal_label; separator=" // ") AS ?venues)
  WHERE {
    # Find editors for a work published by publisher
    ?venue_ wdt:P123 wd:{{ q }} .
    ?venue_ wdt:P98 | wdt:P5769 ?editor .
    # ?venue_ rdfs:label ?venue_label . FILTER(LANG(?venue_label) = 'en')
  } 
  GROUP BY ?editor
} AS %result
WHERE {
  # Label the result
  INCLUDE %result 
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . } 
}
