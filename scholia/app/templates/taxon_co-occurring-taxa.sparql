#title: Taxa that co-occur with the target taxon in the literature
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?count (CONCAT("/topics/",SUBSTR(STR(target:), 32), ",", SUBSTR(STR(?taxon), 32)) AS ?countUrl)
       ?taxon ?taxonLabel (CONCAT("/taxon/", SUBSTR(STR(?taxon), 32)) AS ?taxonUrl)
       ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  SELECT (COUNT(?work) AS ?count) ?taxon (SAMPLE(?work) AS ?example_work) WHERE {
    # Find works for the specific queried topic
	?work wdt:P921 target: .
    
    # Find co-occuring topics
    ?work wdt:P921 ?taxon .
    
    # Filter for taxa
    ?taxon wdt:P105 ?taxonrank .
    
    # Avoid listing the queried topic itself
    FILTER (target: != ?taxon)
  }
  GROUP BY ?taxon
} AS %result
WHERE {
  # Label the results
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,jp,nl,no,ru,sv,zh" . } 
}
ORDER BY DESC(?count)
LIMIT 200
