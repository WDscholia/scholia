PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT
?citations 
?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work), 32)) AS ?workUrl)
(?work AS ?wikidata_url)

WHERE {
  { SELECT (COUNT(?work1) as ?citations) ?work WHERE {
  { SELECT (COUNT(?work) as ?count) ?work WHERE {
  # Works on the topic
    ?work wdt:P921 / (wdt:P361+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) ) target: .
  # Works that do not have a publication date statement
    FILTER NOT EXISTS { ?work wdt:P577 ?date . }
  # Works that do not have an ISSN, i.e. that are not periodicals
    FILTER NOT EXISTS { ?work wdt:P236 ?issn . }
  }
  GROUP BY ?work
  ORDER BY DESC(?count)
  LIMIT 2000 }
  # Works cited
    OPTIONAL { ?work1 wdt:P2860 ?work }.
  }
  GROUP BY ?work
  ORDER BY DESC(?citations)
  LIMIT 200 }
  
  # Label the results
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?citations)