#defaultView:ScatterChart

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT ?work_count ?citation_count ?journal (REPLACE(STR(?journal), ".*/", "") AS ?journalLabel) 

WHERE {
  { SELECT ?work_count ?citation_count ?journal {
    {
      SELECT ?journal (COUNT(?work) AS ?work_count) {
  	    ?journal wdt:P123 target: .
  	    ?work wdt:P1433 ?journal .
      }
      GROUP BY ?journal 
    }
    {
      SELECT ?journal (COUNT(?citing_work) AS ?citation_count) {
  	    ?journal wdt:P123 target: .
  	    ?work wdt:P1433 ?journal .
        ?citing_work wdt:P2860 ?work . 
      }
      GROUP BY ?journal 
    } 
  } }
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . } 
}
