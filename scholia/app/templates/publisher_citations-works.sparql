#defaultView:ScatterChart
SELECT ?work_count ?citation_count ?journal ?journalLabel 
WITH {
  SELECT ?work_count ?citation_count ?journal {
    {
      SELECT ?journal (COUNT(?work) AS ?work_count) {
  	    ?journal wdt:P123 wd:{{ q }} .
  	    ?work wdt:P1433 ?journal .
      }
      GROUP BY ?journal 
    }
    {
      SELECT ?journal (COUNT(?citing_work) AS ?citation_count) {
  	    ?journal wdt:P123 wd:{{ q }} .
  	    ?work wdt:P1433 ?journal .
        ?citing_work wdt:P2860 ?work . 
      }
      GROUP BY ?journal 
    } 
  }
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,jp,nl,no,ru,sv,zh" . } 
}
