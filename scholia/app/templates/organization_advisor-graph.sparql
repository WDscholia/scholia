#defaultView:Graph
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?author1 (REPLACE(STR(?author1), ".*/", "") AS ?author1Label) ?rgb ?author2 (REPLACE(STR(?author2), ".*/", "") AS ?author2Label) WHERE {
  {
    SELECT DISTINCT ?author1 ?author2 ?rgb WHERE {
      VALUES ?organisation { target: }
      {
        ?author1 wdt:P108 ?organisation .
      }
      UNION {
        ?author1 wdt:P1416 [ wdt:P361* ?organisation ] .
      }
      {
        ?author2 wdt:P108 ?organisation .
      }
      UNION {
        ?author2 wdt:P1416 [ wdt:P361* ?organisation ] .
      }
      {
        ?author1 wdt:P184 ?author2
      }
      UNION {
        ?author2 wdt:P185 ?author1
      }
      OPTIONAL {
        ?author1 wdt:P21 ?gender1
      } .
      BIND (IF(?gender1 = wd:Q6581097,"3182BD","E6550D") AS ?rgb)
    }
  }
  # SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
