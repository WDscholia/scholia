PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?count
  
  ?coused ?cousedLabel (CONCAT("/use/", SUBSTR(STR(?coused), 32)) AS ?cousedUrl)
  ("ðŸ”Ž" AS ?zoom)
  (CONCAT("../uses/{{ q }},", SUBSTR(STR(?coused), 32)) AS ?zoomUrl)
  ?cousedDescription
  
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  # Find works that are using a specific software,
  # and find other software used in the found works
  SELECT ?coused (COUNT(DISTINCT ?work) as ?count) (SAMPLE(?work) AS ?example_work)
  WHERE {
    ?work (wdt:P2283 | wdt:P4510) / (wdt:P279*) target: .
    ?work wdt:P2283 | wdt:P4510 ?coused .
    FILTER (?coused != target:)
  }
  GROUP BY ?coused
} AS %result
WHERE {
  # Label the result
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . }
}
ORDER BY DESC(?count) 