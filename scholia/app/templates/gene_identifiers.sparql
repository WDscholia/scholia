PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# title: identifiers for this gene
SELECT
  ?Identifier ?IdentifierLabel
  ?Value (SAMPLE(?idUrls) as ?ValueUrl)
  ?IdentifierDescription
WITH {
  SELECT ?Identifier ?Value ?formatterurl WHERE {
    target: ?IDdir ?Value .
    ?Identifier wikibase:directClaim ?IDdir ;
            wdt:P31 wd:Q42415497 .
    OPTIONAL { 
      ?Identifier wdt:P1630 ?formatterurl .
    }
  } LIMIT 500
} AS %RESULTS {
  {
    SELECT * WHERE {
      INCLUDE %RESULTS
      BIND(IRI(REPLACE(?formatterurl, '\\\\$1', str(?Value))) AS ?idUrls).
    }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?Identifier ?IdentifierLabel ?IdentifierDescription ?Value
ORDER BY ASC(?IdentifierLabel)

