PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?citations ?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work), 32)) AS ?workUrl)
WITH {
  SELECT ?work (COUNT(DISTINCT ?statement) AS ?count) WHERE {
    ?work wdt:P50 target: .
	OPTIONAL { ?statement prov:wasDerivedFrom/pr:P248 ?work . }
  } GROUP BY ?work
} AS %works
WITH {
  SELECT (COUNT(?work1) as ?citations) ?work WHERE {
    INCLUDE %works
    FILTER (?count = 0)
    # Works cited
    OPTIONAL { ?work1 wdt:P2860 ?work }.
  }
  GROUP BY ?work
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . }
} ORDER BY DESC(?citations)
  LIMIT 200
