{% import 'sparql-helpers.sparql' as sparql_helpers -%}
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?citations ?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work),32)) AS ?workUrl) WHERE {
  {
    SELECT (COUNT(?work1) AS ?citations) ?work WHERE {
      {
        SELECT ?work (COUNT(DISTINCT ?statement) AS ?count) WHERE {
          ?work wdt:P50 target: .
          OPTIONAL {
            ?statement prov:wasDerivedFrom/pr:P248 ?work .
          }
        }
        GROUP BY ?work
      }
      FILTER (?count = 0)
      # Works cited
      OPTIONAL {
        ?work1 wdt:P2860 ?work
      } .
    }
    GROUP BY ?work
  }
  {{ sparql_helpers.labels(["?work"], languages) }}
}
ORDER BY DESC(?citations)
LIMIT 200
