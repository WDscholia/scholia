PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# title: related chemical structures
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT ?mol (REPLACE(STR(?mol), ".*/", "") AS ?molLabel) ?InChIKey ?CAS ?ChemSpider ?PubChem_CID WHERE {
  target: wdt:P235 ?queryKey .
  ?mol wdt:P235 ?InChIKey .
  OPTIONAL { ?mol wdt:P231 ?CAS }
  OPTIONAL { ?mol wdt:P661 ?ChemSpider }
  OPTIONAL { ?mol wdt:P662 ?PubChem_CID }
  FILTER (regex(str(?InChIKey), concat("^", substr($queryKey,1,14), "-")))
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
