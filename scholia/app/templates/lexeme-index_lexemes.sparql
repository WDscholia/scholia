SELECT 
  ?count
  ?lexeme (?lexeme_label AS ?lexemeLabel)
  ?language ?languageLabel
  ?example_work ?example_workLabel ?example_workDescription
WHERE {
  {
    SELECT
      (COUNT(DISTINCT ?work) AS ?count)
      ?lexeme
      (SAMPLE(?language_) AS ?language) 
      (SAMPLE(?work) AS ?example_work)
    WHERE {
      { ?work wdt:P6254 ?lexeme . }
      UNION
      { ?lexeme wdt:P1343 ?work ; dct:language [] . }
      ?lexeme dct:language ?language_ .
    }
    GROUP BY ?lexeme
    ORDER BY DESC(?count)
    LIMIT 500
  }
  ?lexeme wikibase:lemma ?lexeme_label
  SERVICE wikibase:label { bd:serviceParam wikibase:language "{{ g.lang_code }},[AUTO_LANGUAGE],en,cz,da,de,es,fr,jp,nl,no,pl,ru,sv,zh" . }
}
ORDER BY DESC(?count)