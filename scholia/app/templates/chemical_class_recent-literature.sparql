SELECT ?date ?work ?workLabel ?type ?via ?topics
WITH {
  SELECT DISTINCT (MAX(?dates) as ?datetime) ?work ?via WHERE {
    { VALUES ?topic { wd:{{ q }}}
      ?work wdt:P921 ?topic . BIND ("main topic" AS ?via) }
    UNION
    { ?work wdt:P921 ?topic . ?topic ((^wdt:P361)+) wd:{{ q }} . BIND ("part" AS ?via) }
    UNION
    { ?work wdt:P921 ?topic . ?topic (wdt:P31* / wdt:P279* ) wd:{{ q }} . BIND ("subclass" AS ?via). FILTER (?topic != wd:{{ q }}) }
    UNION
    { ?work wdt:P921 ?topic . ?topic (wdt:P1269+ ) wd:{{ q }} . BIND ("facet" AS ?via)}
    UNION
    {
      wd:{{ q }} ?propp ?statement .
      ?statement a wikibase:BestRank ;
                 prov:wasDerivedFrom/pr:P248 ?work .
      BIND ("statement" AS ?via)
    }
    OPTIONAL { ?work wdt:P577 ?dateLits . BIND(xsd:date(?dateLits) AS ?dates) }
  }
  GROUP BY ?work ?via
  ORDER BY DESC(?datetime)
  LIMIT 250
} AS %works
WITH {
  SELECT (MAX(?dates) as ?datetime) ?work (GROUP_CONCAT(DISTINCT ?type_label; separator=", ") AS ?type) ?via (GROUP_CONCAT(DISTINCT ?topic_label; separator=" // ") AS ?topics) WHERE {
    INCLUDE %works
    ?work wdt:P921 ?topic . 
    OPTIONAL { ?work wdt:P31 ?type_ . ?type_ rdfs:label ?type_label . FILTER (LANG(?type_label) = 'en') }
    OPTIONAL { ?work wdt:P577 ?dates . }
    ?topic rdfs:label ?topic_label .  FILTER (lang(?topic_label) = 'en')
  }
  GROUP BY ?work ?via
} AS %result
WHERE {
  INCLUDE %result
  BIND(xsd:date(?datetime) AS ?date)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,jp,nl,no,ru,sv,zh". }
}
ORDER BY DESC(?date)
LIMIT 500

