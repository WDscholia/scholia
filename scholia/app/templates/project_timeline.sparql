#defaultView:Timeline{"hide": ["?end_time", "?start_time"]}
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT DISTINCT ?start_time ?end_time ?description WHERE {
  {
    target: (wdt:P571 | wdt:P580) ?start_time ;
            (wdt:P576 | wdt:P582) ?end_time .
    BIND ("Project time" AS ?description)
  }
  UNION {
    target: (^wdt:P1344 | wdt:P710)+ ?project .
    ?project (wdt:P571 | wdt:P580) ?start_time ;
             (wdt:P576 | wdt:P582) ?end_time .
    ?project rdfs:label ?projectName .
    FILTER (LANG(?projectName) = "en")
    BIND (?projectName AS ?description)
  }
  UNION {
    ?work wdt:P859/(wdt:P1344 | ^wdt:P710)* target: ;
          wdt:P577 ?start_time .
    BIND ("Publication" AS ?description)
  }
  UNION {
    ?event wdt:P859 target: .
    ?event wdt:P585 | wdt:P580 ?start_time .
    BIND ("ðŸ“… Sponsored event" AS ?description)
  }
}
