#defaultView:BarChart
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT (STR(?year_) AS ?year) (SUM(?count_) AS ?count) ?kind WHERE {
  {
    VALUES ?year_class { wd:Q577 wd:Q3186692 }
    [] wdt:P31 ?year_class ;
       wdt:P585 ?date .
    BIND (YEAR(?date) AS ?year_)
    BIND (0 AS ?count_)
    BIND ("_" AS ?kind)

    {
      SELECT (MIN(?year) as ?earliest_year) WHERE {
        target: wdt:P2860|^wdt:P2860 ?work .
        ?work wdt:P577 ?date .
        BIND (YEAR(?date) AS ?year)
      }
    }

    FILTER(?year_ >= ?earliest_year && ?year_ <= YEAR(NOW()))
  }
  UNION {
    SELECT ?year_ (COUNT(DISTINCT ?citing_work) AS ?count_) ?kind WHERE {
      ?citing_work wdt:P2860 target: .
      # Detect self-citations
      BIND (IF(EXISTS {
        target: wdt:P50 ?selfauthor .
        ?citing_work wdt:P50 ?selfauthor
      },"detected incoming self-citations","citations from others or non-detected self-citations") AS ?kind)
      # Year of citation
      ?citing_work wdt:P577 ?date .
      BIND (YEAR(?date) AS ?year_)
    }
    GROUP BY ?year_ ?kind
  }
  UNION {
    SELECT ?year_ (COUNT(DISTINCT ?cited_work) AS ?count_) ?kind WHERE {
      target: wdt:P2860 ?cited_work .
      # Detect self-citations
      BIND (IF(EXISTS {
        ?cited_work wdt:P50 ?selfauthor .
        target: wdt:P50 ?selfauthor
      },"detected outgoing self-citations","outgoing citations to others or non-detected self-citations") AS ?kind)
      # Year of citation
      ?cited_work wdt:P577 ?date .
      BIND (YEAR(?date) AS ?year_)
    }
    GROUP BY ?year_ ?kind
  }
}
GROUP BY ?year_ ?kind
ORDER BY DESC(?year_)
