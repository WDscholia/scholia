#defaultView:BarChart
SELECT
  (STR(?year_) AS ?year)
  (SUM(?count_) AS ?count)
  ?kind
WHERE {
  {
    VALUES ?year_ { 2000 2001 2002 2003 2004 2005 2006 2007 2008 2009
                   2010 2011 2012 2013 2014 2015 2016 2017 2018 2019 }
    BIND(0 AS ?count_)
    BIND("_" AS ?kind)
  }
  UNION 
  {
    SELECT
      ?year_
      (COUNT(DISTINCT ?citing_work) AS ?count_)
      ?kind
    WHERE {
      ?citing_work wdt:P2860 wd:{{q}} .
  
      # Detect self-citations
      BIND(IF(EXISTS { wd:{{q}} wdt:P50 ?selfauthor . ?citing_work  wdt:P50 ?selfauthor } ,
        "detected incoming self-citations",
        "citations from others or non-detected self-citations") AS ?kind)

      # Year of citation
      ?citing_work wdt:P577 ?date .
      BIND(YEAR(?date) AS ?year_)
    }
    GROUP BY ?year_ ?kind
  }
  UNION 
  {
    SELECT
      ?year_
      (COUNT(DISTINCT ?cited_work) AS ?count_)
      ?kind
    WHERE {
      wd:{{q}} wdt:P2860 ?cited_work .
  
      # Detect self-citations
      BIND(IF(EXISTS { ?cited_work wdt:P50 ?selfauthor . wd:{{q}} wdt:P50 ?selfauthor },
        "detected outgoing self-citations",
        "outgoing citations to others or non-detected self-citations") AS ?kind)

      # Year of citation
      ?cited_work wdt:P577 ?date .
      BIND(YEAR(?date) AS ?year_)
    }
    GROUP BY ?year_ ?kind
  }
}
GROUP BY ?year_ ?kind
ORDER BY DESC(?year_)