{% import 'sparql-helpers.sparql' as sparql_helpers -%}

#title: List of authors for a work
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT
  # Author order
  ?order ?academic_age
  # Author item and label
  ?author ?authorUrl ?authorDescription
WHERE {
  {
    target: p:P50 ?statement .
    ?statement ps:P50 ?_author .

    {{ sparql_helpers.labels(["?_author"], languages) }}
    {{ sparql_helpers.descriptions(["?_author"], languages) }}
    BIND (?_authorLabel AS ?author)
    BIND (?_authorDescription AS ?authorDescription)
    BIND (CONCAT("../author/", SUBSTR(STR(?_author), 32)) AS ?authorUrl)

    OPTIONAL {
      SELECT ?_author (MAX(?academic_age_) AS ?academic_age) {
        target: wdt:P50 ?_author ;
                wdt:P577 ?publication_date .
        [] wdt:P31/wdt:P279* wd:Q55915575 ;
          wdt:P50 ?_author ;
          wdt:P577 ?other_publication_date .
        BIND (YEAR(?publication_date) - YEAR(?other_publication_date) AS ?academic_age_)
      } GROUP BY ?_author
    }
  } UNION {
    target: p:P2093 ?statement .
    ?statement ps:P2093 ?_author
    BIND (CONCAT(?_author, " â†—") AS ?author)
    BIND (CONCAT("https://author-disambiguator.toolforge.org/names_oauth.php?doit=Look+for+author&name=", ENCODE_FOR_URI(?_author)) AS ?authorUrl)
  }

  OPTIONAL { ?statement pq:P1545 ?order_ . }
  BIND (xsd:integer(?order_) AS ?order)
}
ORDER BY ?order
