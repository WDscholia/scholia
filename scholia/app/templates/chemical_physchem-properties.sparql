{% import 'sparql-helpers.sparql' as sparql_helpers -%}
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
# title: physicochemical properties of this chemical
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT DISTINCT ?propEntity ?propEntityLabel ?value ?units ?unitsLabel ?qualifiers ?source ?sourceLabel ?doi WHERE {
  {
    SELECT DISTINCT ?propEntity ?value ?units ?source ?doi (GROUP_CONCAT(?qualifierStr; SEPARATOR=", ") AS ?qualifiers) WHERE {
      {
        SELECT DISTINCT ?propEntity ?statement ?value ?units (SAMPLE (?source_) AS ?source) (SAMPLE( ?doi_) AS ?doi) WHERE {
          { 
            target: ?propp ?statement .
            ?statement a wikibase:BestRank ; ?proppsv [ wikibase:quantityAmount ?value ; wikibase:quantityUnit ?units ] .
          } UNION {
            target: ?propp ?statement .
            ?statement a wikibase:BestRank ; ?proppsv [ wikibase:quantityAmount ?value ; wikibase:quantityUnit ?units ] .
            ?statement prov:wasDerivedFrom/pr:P248 ?source_ .
          } UNION {
            target: ?propp ?statement .
            ?statement a wikibase:BestRank ; ?proppsv [ wikibase:quantityAmount ?value ; wikibase:quantityUnit ?units ] .
            ?statement prov:wasDerivedFrom/pr:P248 ?source_ .
            ?source_ wdt:P356 ?doi_ .
          }
          ?property wikibase:claim ?propp ;
                wikibase:statementValue ?proppsv ;
                wdt:P1629 ?propEntity ;
                wdt:P31 wd:Q21077852 .
        } GROUP BY ?propEntity ?value ?units ?statement
      } 
      OPTIONAL {      
          ?property wikibase:claim ?propp ;
                    wdt:P1629 ?propEntity .
          { 
            target: ?propp ?statement .
            ?statement ?qualifier ?qualifierVal .
            ?qualifier a owl:DatatypeProperty .
            ?qualifierProp wikibase:qualifier ?qualifier ;
                         rdfs:label ?qProplabel ; FILTER (LANG(?qProplabel) = "en") .
            BIND (CONCAT(STR(?qProplabel), ": ", STR(?qualifierVal)) AS ?qualifierStr)
          } UNION {
            target: ?propp ?statement .
            ?statement ?qualifier ?qualifierVal .
            ?qualifier a owl:ObjectProperty .
            ?qualifierVal rdfs:label ?qVallabel ; FILTER (LANG(?qVallabel) = "en") .
            ?qualifierProp wikibase:qualifier ?qualifier ;
                         rdfs:label ?qProplabel ; FILTER (LANG(?qProplabel) = "en") .
            BIND (CONCAT(STR(?qProplabel), ": ", STR(?qVallabel)) AS ?qualifierStr)
          }
       }
    }
    GROUP BY ?propEntity ?value ?units ?source ?doi
  }
  {{ sparql_helpers.labels(["?propEntity", "?units"], languages) }}
  {{ sparql_helpers.labels_unbound(["?source"], languages) }}
}
ORDER BY ASC(?propEntityLabel)
