{% import 'sparql-helpers.sparql' as sparql_helpers -%}
PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?number_of_works ?number_of_authors ?organization ?organizationLabel (CONCAT("/organization/", SUBSTR(STR(?organization),32)) AS ?organizationUrl) ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work),32)) AS ?example_workUrl) WHERE {
  {
    SELECT ?organization (COUNT(DISTINCT ?work) AS ?number_of_works) (COUNT(DISTINCT ?author) AS ?number_of_authors) (SAMPLE(?work) AS ?example_work) WHERE {
      {
        SELECT ?work WHERE {
          ?work wdt:P921/(wdt:P361 | wdt:P1269 | wdt:P31 | wdt:P279)* target2:
        }
      }
      ?work wdt:P50 ?author .
      ?author (wdt:P108 | wdt:P1416) ?organization .
      ?organization wdt:P17 target1: .
    }
    GROUP BY ?organization
  }
  {{ sparql_helpers.labels(["?organization", "?example_work"], languages) }}
}
ORDER BY DESC(?number_of_works)
