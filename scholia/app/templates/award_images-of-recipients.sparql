#defaultView:ImageGrid
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?year
  ?recipient ?recipientLabel 
  ?image
WITH {
  SELECT ?recipient ?year (SAMPLE(?image_) AS ?image) WHERE {
    ?recipient p:P166 ?award_statement . 
    ?award_statement ps:P166 target: .
    OPTIONAL {
      ?award_statement pq:P585 ?time .
	  BIND(YEAR(?time) AS ?year)
	}
    ?recipient wdt:P18 ?image_ .
  }
  GROUP BY ?recipient ?year
} AS %result
WHERE {
  INCLUDE %result 
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . }  
}
ORDER BY DESC(?year)
  
