PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT DISTINCT ?description ?value ?valueUrl
WHERE {
  BIND(target: AS ?work)
  {
    BIND(1 AS ?order)
    BIND("Title" AS ?description)
    ?work wdt:P1476 ?value .
  }
  UNION
  {
    BIND(1.5 AS ?order)
    BIND("Type" AS ?description)
    ?work wdt:P31 ?iri .
    BIND(SUBSTR(STR(?iri), 32) AS ?q) 
    ?iri rdfs:label ?value_string . 
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?q) AS ?value)
  }
  UNION
  {
    SELECT
      (2 AS ?order)
      ("Authors" AS ?description)
      (GROUP_CONCAT(?value_; separator=", ") AS ?value)
      (CONCAT("../authors/", GROUP_CONCAT(?q; separator=",")) AS ?valueUrl)
    {
      BIND(1 AS ?dummy)
      target: wdt:P50 ?iri .
      BIND(SUBSTR(STR(?iri), 32) AS ?q) 
      ?iri rdfs:label ?value_string . 
      FILTER (LANG(?value_string) = 'en')
      BIND(COALESCE(?value_string, ?q) AS ?value_)
    }
    GROUP BY ?dummy
  }
  UNION
  {
    SELECT
      (3 AS ?order)
      ("Editors" AS ?description)
      (GROUP_CONCAT(?value_; separator=", ") AS ?value)
      (CONCAT("../authors/", GROUP_CONCAT(?q; separator=",")) AS ?valueUrl)
    {
      BIND(1 AS ?dummy)
      target: wdt:P98 ?iri .
      BIND(SUBSTR(STR(?iri), 32) AS ?q) 
      ?iri rdfs:label ?value_string . 
      FILTER (LANG(?value_string) = 'en')
      BIND(COALESCE(?value_string, ?q) AS ?value_)
    }
    GROUP BY ?dummy
  }
  UNION
  {
    BIND(2.5 AS ?order)
    BIND("Language" AS ?description)
    ?work wdt:P407 ?iri .
    BIND(SUBSTR(STR(?iri), 32) AS ?q) 
    ?iri rdfs:label ?value_string .
    FILTER (LANG(?value_string) = 'en')
    BIND(STR(?value_string) AS ?value)
    BIND(CONCAT("../language/", ?q) AS ?valueUrl)
  }
  UNION
  {
    SELECT
      (3 AS ?order)
      ("Reviewers" AS ?description)
      (GROUP_CONCAT(?value_; separator=", ") AS ?value)
      (CONCAT("../authors/", GROUP_CONCAT(?q; separator=",")) AS ?valueUrl)
    {
      BIND(1 AS ?dummy)
      target: wdt:P4032 ?iri .
      BIND(SUBSTR(STR(?iri), 32) AS ?q) 
      ?iri rdfs:label ?value_string . 
      FILTER (LANG(?value_string) = 'en')
      BIND(COALESCE(?value_string, ?q) AS ?value_)
    }
    GROUP BY ?dummy
  }
  UNION
  {
    BIND(4 AS ?order)
    BIND("Published in" AS ?description)
    ?work wdt:P1433 ?iri .
    BIND(SUBSTR(STR(?iri), 32) AS ?q) 
    ?iri rdfs:label ?value_string . 
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?q) AS ?value)
    BIND(CONCAT("../venue/", ?q) AS ?valueUrl)
  }
  UNION
  {
    BIND(4 AS ?order)
    BIND("Series" AS ?description)
    ?work wdt:P179 ?iri .
    BIND(SUBSTR(STR(?iri), 32) AS ?q) 
    ?iri rdfs:label ?value_string . 
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?q) AS ?value)
    BIND(CONCAT("../series/", ?q) AS ?valueUrl)
  }
  UNION
  {
    BIND(6 AS ?order)
    BIND("Publication date" AS ?description)
    ?work p:P577 / psv:P577 ?publication_date_value .
    ?publication_date_value wikibase:timePrecision ?time_precision ;
                            wikibase:timeValue ?publication_date .
    BIND(IF(?time_precision = 9, YEAR(?publication_date), xsd:date(?publication_date)) AS ?value)
  }
  UNION
  {
    BIND(7 AS ?order)
    BIND("Publisher" AS ?description)
    ?work wdt:P123 ?iri .
    BIND(SUBSTR(STR(?iri), 32) AS ?q) 
    ?iri rdfs:label ?value_string . 
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?q) AS ?value)
    BIND(CONCAT("../publisher/", ?q) AS ?valueUrl)
  }
  UNION
  {
    SELECT
      (8 AS ?order)
      ("Topics" AS ?description)
      (GROUP_CONCAT(?value_; separator=", ") AS ?value)
      (CONCAT("../topics/", GROUP_CONCAT(?q; separator=",")) AS ?valueUrl)
    {
      BIND(1 AS ?dummy)
      target: wdt:P921 ?iri .
      BIND(SUBSTR(STR(?iri), 32) AS ?q) 
      ?iri rdfs:label ?value_string . 
      FILTER (LANG(?value_string) = 'en')
      BIND(COALESCE(?value_string, ?q) AS ?value_)
    }
    GROUP BY ?dummy
  }
  UNION
  {
    SELECT
      (9 AS ?order)
      ("Uses" AS ?description)
      (GROUP_CONCAT(?value_; separator=", ") AS ?value)
      (CONCAT("../uses/", GROUP_CONCAT(?q; separator=",")) AS ?valueUrl)
    {
      BIND(1 AS ?dummy)
      target: wdt:P4510 ?iri .
      BIND(SUBSTR(STR(?iri), 32) AS ?q) 
      ?iri rdfs:label ?value_string . 
      FILTER (LANG(?value_string) = 'en')
      BIND(COALESCE(?value_string, ?q) AS ?value_)
    }
    GROUP BY ?dummy
  }
  UNION
  {
    BIND(10 AS ?order)
    BIND("DOI" AS ?description)
    ?work wdt:P356 ?valueUrl_ .
    BIND(CONCAT("https://doi.org/", ENCODE_FOR_URI(?valueUrl_)) AS ?valueUrl)
    BIND(CONCAT(?valueUrl_, " ‚Üó") AS ?value)
  }
  UNION
  {
    BIND(11 AS ?order)
    BIND("Homepage" AS ?description)
    ?work wdt:P856 ?valueUrl .
    BIND(STR(?valueUrl) AS ?value)
  }
  UNION
  {
    BIND(12 AS ?order)
    BIND("Full text" AS ?description)
    ?work wdt:P953 ?valueUrl .
    BIND(CONCAT(STR(?valueUrl), " ‚Üó") AS ?value)
  }
  UNION
  {
    BIND(13 AS ?order)
    BIND("üõë Retracted" AS ?description)
    { ?work wdt:P31 wd:Q45182324 . }
    UNION
    { ?work wdt:P793 wd:Q7316896 . }
  }
  UNION
  {
    BIND(14 AS ?order)
    BIND("üõë Retracted by" AS ?description)
    ?work wdt:P5824 ?iri .
    BIND(SUBSTR(STR(?iri), 32) AS ?q) 
    ?iri rdfs:label ?value_string . 
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?q) AS ?value)
    BIND(CONCAT("../work/", ?q) AS ?valueUrl)
  }
  UNION
  {
    BIND(15 AS ?order)
    BIND("‚ö†Ô∏è Preprint" AS ?description)
    { ?work p:P31 ?statement . ?statement ps:P31 wd:Q580922 . MINUS { ?statement pq:P642 [] } }
  }
  UNION
  {
    SELECT (COUNT(DISTINCT ?citationStatement) AS ?value) ?order ?description ?valueUrl WHERE {
      VALUES ?work { wd:{{ q }} }
      VALUES ?intention { wd:Q96472102 wd:Q101149476 }
      ?work ^ps:P2860 ?citationStatement . ?citationStatement pq:P3712 ?intention .
      BIND(20 AS ?order)
      BIND("Data or method used by" AS ?description)
      BIND("./{{ q }}/cito" AS ?valueUrl)
    } GROUP BY ?order ?description ?valueUrl
  }
  UNION
  {
    SELECT (COUNT(DISTINCT ?citationStatement) AS ?value) ?order ?description ?valueUrl WHERE {
      VALUES ?work { wd:{{ q }} }
      VALUES ?intention { wd:Q107687829 wd:Q107710355 }
      ?work ^ps:P2860 ?citationStatement . ?citationStatement pq:P3712 ?intention .
      BIND(21 AS ?order)
      BIND("Disagreed with by" AS ?description)
      BIND("./{{ q }}/cito" AS ?valueUrl)
    } GROUP BY ?order ?description ?valueUrl
  }
  UNION
  {
    BIND(30 AS ?order)
    BIND("‚ö†Ô∏è Preprint of" AS ?description)
    { ?work p:P31 ?statement .
      ?statement pq:P642 ?pub ; ps:P31 wd:Q580922 .
    BIND(SUBSTR(STR(?pub), 32) AS ?pubqid) 
    ?pub rdfs:label ?value_string . 
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?pubqid) AS ?value)
    BIND(CONCAT("../work/", ?pubqid) AS ?valueUrl)}
  }
  UNION
  {
    BIND(31 AS ?order)
    BIND("‚ö†Ô∏è Cites retracted article" AS ?description)
    { ?work wdt:P2860 ?retracted . ?retracted wdt:P31 wd:Q45182324 . }
    UNION
    { ?work wdt:P2860 ?retracted . ?retracted wdt:P793 wd:Q7316896 . }
    UNION
    { ?work wdt:P2860 ?retracted . ?retracted wdt:P5824 [] . }
    ?retracted rdfs:label ?value_string . 
    FILTER (LANG(?value_string) = 'en')
    BIND(SUBSTR(STR(?retracted), 32) AS ?q) 
    BIND(COALESCE(?value_string, ?q) AS ?value)
    BIND(CONCAT("../work/", ?q) AS ?valueUrl)
  }
  UNION
  {
    BIND(32 AS ?order)
    BIND("Preprint" AS ?description)
    { ?preprint p:P31 ?statement .
      ?statement pq:P642 ?work ; ps:P31 wd:Q580922 .
    BIND(SUBSTR(STR(?preprint), 32) AS ?pubqid)
    ?preprint rdfs:label ?value_string .
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?pubqid) AS ?value)
    BIND(CONCAT("../work/", ?pubqid) AS ?valueUrl)}
  }
  UNION
  {
    BIND(33 AS ?order)
    BIND("‚ö†Ô∏è Erratum of" AS ?description)
    { ?work p:P31 ?statement .
      ?statement pq:P642 ?pub ; ps:P31 wd:Q1348305 .
    BIND(SUBSTR(STR(?pub), 32) AS ?pubqid)
    ?pub rdfs:label ?value_string .
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?pubqid) AS ?value)
    BIND(CONCAT("../work/", ?pubqid) AS ?valueUrl)}
  }
  UNION
  {
    BIND(34 AS ?order)
    BIND("‚ö†Ô∏è Retracts" AS ?description)
    {
    { ?work p:P31 ?statement .
      ?statement pq:P642 ?pub ; ps:P31 wd:Q7316896 . }
    UNION
    { ?work ^wdt:P5824 ?pub . }
    BIND(SUBSTR(STR(?pub), 32) AS ?pubqid)
    ?pub rdfs:label ?value_string .
    FILTER (LANG(?value_string) = 'en')
    BIND(COALESCE(?value_string, ?pubqid) AS ?value)
    BIND(CONCAT("../work/", ?pubqid) AS ?valueUrl)}
  }
} 
ORDER BY ?order
