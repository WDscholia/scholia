PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?publication_date ?podcast ?podcastLabel (CONCAT("/podcast/", SUBSTR(STR(?podcast),32)) AS ?podcastUrl) ?episode ?episodeLabel (CONCAT("/podcast-episode/", SUBSTR(STR(?episode),32)) AS ?episodeUrl) WHERE {
  {
    SELECT DISTINCT ?episode ?podcast (MIN(?publication_date_) AS ?publication_date) WHERE {
      ?episode wdt:P31 wd:Q61855877 ;
               wdt:P179 ?podcast .
      ?podcast wdt:P407 target: .
      OPTIONAL {
        ?episode wdt:P577 ?publication_datetime .
        BIND (xsd:date(?publication_datetime) AS ?publication_date_)
      }
    }
    GROUP BY ?episode ?podcast
    ORDER BY DESC(?publication_date)
    LIMIT 50
  }
  # SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
}
GROUP BY ?publication_date ?episode ?episodeLabel ?podcast ?podcastLabel
ORDER BY DESC(?publication_date)
