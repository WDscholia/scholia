#defaultView:Timeline
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
# Timeline for an author
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT ?label ?beginning ?ending ?education_degree_label WHERE {
  # { Birth target: wdt:P569 ?beginning . BIND("Birth" AS ?label) } UNION
  {
    {
      SELECT ?work ?beginning WHERE {
        {
          SELECT ?work ?beginning (COUNT(?citing_article) AS ?number_of_citations) WHERE {
            {
              SELECT ?work (MIN(?publication_date) AS ?beginning) WHERE {
                ?work wdt:P50 target: .
                ?work wdt:P577 ?publication_date .
              }
              GROUP BY ?work
            }
            OPTIONAL {
              ?work wdt:P50 target: .
              ?citing_article wdt:P2860 ?work .
            }
          }
          GROUP BY ?work ?beginning
        }
      }
      ORDER BY DESC(?number_of_citations)
      LIMIT 1
    }
    BIND ("üìñ publication of most cited article" AS ?label)
  }
  UNION {
    {
      SELECT ?work ?beginning WHERE {
        {
          SELECT ?work ?beginning (COUNT(?citing_article) AS ?number_of_citations) WHERE {
            {
              SELECT ?work (MIN(?publication_date) AS ?beginning) WHERE {
                ?work wdt:P50 target: .
                ?work wdt:P577 ?publication_date .
              }
              GROUP BY ?work
            }
            OPTIONAL {
              ?work wdt:P50 target: .
              ?citing_article wdt:P2860 ?work .
            }
          }
          GROUP BY ?work ?beginning
        }
      }
      ORDER BY ?beginning
      LIMIT 1
    }
    BIND ("üìñ first publication" AS ?label)
  }
  UNION {
    {
      SELECT ?work ?beginning WHERE {
        {
          SELECT ?work ?beginning (COUNT(?citing_article) AS ?number_of_citations) WHERE {
            {
              SELECT ?work (MIN(?publication_date) AS ?beginning) WHERE {
                ?work wdt:P50 target: .
                ?work wdt:P577 ?publication_date .
              }
              GROUP BY ?work
            }
            OPTIONAL {
              ?work wdt:P50 target: .
              ?citing_article wdt:P2860 ?work .
            }
          }
          GROUP BY ?work ?beginning
        }
      }
      ORDER BY DESC(?beginning)
      LIMIT 1
    }
    BIND ("üìñ latest publication" AS ?label)
  }
  UNION {
    # Education
    target: p:P69 ?education_statement .
    ?education_statement ps:P69 ?education .
    ?education rdfs:label ?education_label .
    FILTER (LANG(?education_label) = 'en')
    BIND (CONCAT("ü¶â ", ?education_label) AS ?label)
    OPTIONAL {
	  target: p:P69 ?education_statement .
      ?education_statement pq:P580 ?beginning .
    }
    OPTIONAL {
	  target: p:P69 ?education_statement .
      ?education_statement pq:P582 ?ending .
    }
    OPTIONAL {
	  target: p:P69 ?education_statement .
      ?education_statement pq:P512 ?education_degree .
      ?education_degree rdfs:label ?education_degree_label .
      FILTER (LANG(?education_degree_label) = 'en')
    }
  }
  UNION {
    # Affiliation
    target: p:P1416 | p:P108 ?affiliation_statement .
    ?affiliation_statement ps:P1416 | ps:P108 ?affiliation .
    ?affiliation rdfs:label ?affiliation_label .
    FILTER (LANG(?affiliation_label) = 'en')
    BIND (CONCAT("üè† ", ?affiliation_label) AS ?label)
    # OPTIONAL { ?affiliation wdt:P18 ?image }
    OPTIONAL {
	  target: p:P1416 | p:P108 ?affiliation_statement .
      ?affiliation_statement pq:P580 ?beginning .
    }
    OPTIONAL {
	  target: p:P1416 | p:P108 ?affiliation_statement .
      ?affiliation_statement pq:P582 ?ending .
    }
    OPTIONAL {
	  target: p:P1416 | p:P108 ?affiliation_statement .
      ?affiliation_statement pq:P512 ?affiliation_degree .
      ?affiliation_degree rdfs:label ?affiliation_degree_label .
      FILTER (LANG(?affiliation_degree_label) = 'en')
    }
  }
  UNION {
    target: p:P166 ?award_statement .
    ?award_statement ps:P166 ?award .
    ?award rdfs:label ?award_label .
    FILTER (LANG(?award_label) = 'en')
    BIND (CONCAT("üèÜ ", ?award_label) AS ?label)
    ?award_statement pq:P585 ?beginning .
  }
  UNION {
    target: wdt:P569 ?beginning .
    BIND (CONCAT("üë∂ born ") AS ?label) .
  }
  UNION {
    target: wdt:P570 ?beginning .
    BIND (CONCAT("ü•Ä died") AS ?label) .
  }
}
