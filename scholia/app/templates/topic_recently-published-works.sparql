{% import 'sparql-helpers.sparql' as sparql_helpers -%}

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?date ?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work),32)) AS ?workUrl) ?topicsUrl ?topics WHERE {
  {
    SELECT (MAX(?dates) AS ?datetime) ?work (GROUP_CONCAT(DISTINCT ?topicLabel; SEPARATOR=" // ") AS ?topics) (CONCAT("../topics/", GROUP_CONCAT(DISTINCT SUBSTR(STR(?topic),32); SEPARATOR=",")) AS ?topicsUrl) WHERE {
      {
        SELECT DISTINCT ?work WHERE {
          ?work wdt:P921/(wdt:P361+ | wdt:P1269+ | (wdt:P31*/wdt:P279*)) target: .
        }
      }
      ?work wdt:P921 ?topic .
      OPTIONAL {
        ?work wdt:P577 ?dates .
      }
      {{ sparql_helpers.labels(["?topic"], languages) }}
    }
    GROUP BY ?work
  }
  # There is a problem with BC dates
  # BIND(xsd:date(?datetime) AS ?date)
  BIND (REPLACE(STR(?datetime), 'T.*', '') AS ?date)

  {{ sparql_helpers.labels(["?work"], languages) }}
}
GROUP BY ?date ?work ?workLabel ?topicsUrl ?topics
ORDER BY DESC(?date)
LIMIT 500
