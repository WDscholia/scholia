#defaultView:BarChart
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT DISTINCT ?year (COUNT(?work) AS ?number_of_publications) ?role WHERE {
  {
    SELECT DISTINCT ?work (MIN(?years) AS ?year) ?type_ ?role WHERE {
      {
        wd:Q96471816 ^wdt:P31 / ^pq:P3712 ?citesStatement .
        ?citesStatement ^p:P2860 ?work .
        ?work wdt:P31 wd:Q109229154 ;
              wdt:P1433 ?venue_ .
         ?venue_ rdfs:label ?venue . FILTER (LANG(?venue) = "en" || LANG(?venue) = "mul")
        MINUS { ?venue_ wdt:P31 wd:Q1143604 }
        BIND ("explicit" AS ?type_)
      } UNION {
        wd:Q96471816 ^wdt:P31 / ^pq:P3712 ?citesStatement .
        ?citesStatement ^p:P2860 ?work .
        ?citesStatement prov:wasDerivedFrom ?reference .
        ?reference pr:P12545 ?referenceURL .
        BIND ("nanopub" AS ?type_)
        BIND ("Nanopublication" AS ?venue)
      } UNION {
        wd:Q96471816 ^wdt:P31 / ^pq:P3712 ?citesStatement .
        ?citesStatement ^p:P2860 ?work .
        MINUS { ?work wdt:P31 wd:Q109229154 }
        MINUS { ?citesStatement prov:wasDerivedFrom / pr:P12545 [] }
        BIND ("other source" AS ?venue)
      }
      BIND (COALESCE(IF(BOUND(?type_),?venue,'other source')) AS ?role)
      ?work wdt:P577 ?dates .
      BIND (STR(YEAR(?dates)) AS ?years) .
    } GROUP BY ?work ?type_ ?role
  }
} GROUP BY ?year ?role
