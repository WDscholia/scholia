# Prolific authors for a specific project
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?count ?author (REPLACE(STR(?author), ".*/", "") AS ?authorLabel) ?orcid ?example_work (REPLACE(STR(?example_work), ".*/", "") AS ?example_workLabel) WHERE {
  {
    SELECT ?author (COUNT(?work) AS ?count) (SAMPLE(?work) AS ?example_work) WHERE {
      {
        SELECT DISTINCT ?work WHERE {
          {
            ?work wdt:P921/(wdt:P527+ | wdt:P1269+ | (wdt:P31*/wdt:P279*)) target: .
          }
          UNION {
            ?work wdt:P859/(wdt:P1344 | ^wdt:P710)* target: .
          }
          UNION {
            target: ?propp ?statement .
            ?statement a wikibase:BestRank ;
                       prov:wasDerivedFrom/pr:P248 ?work .
          }
        }
      }
      ?work wdt:P50 ?author .
    }
    GROUP BY ?author
  }
  # Include optional ORCID iD
  OPTIONAL {
    ?author wdt:P496 ?orcid .
  }
  # Label the results
  # SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?count)
LIMIT 50
