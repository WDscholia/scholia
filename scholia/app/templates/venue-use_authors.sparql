PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>

SELECT
  ?number_of_works
  
  ?author ?authorLabel
  (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  ?authorDescription
  
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  SELECT 
    (COUNT(?work) AS ?number_of_works)
    ?author 
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    ?work wdt:P1433 target1: ;
          wdt:P4510 target2: ;
          wdt:P50 ?author .
  }
  GROUP BY ?author
  ORDER BY DESC(?number_of_works)
  LIMIT 500
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,da,de,es,fr,it,sv,uk,zh". }
}
ORDER BY DESC(?number_of_works)
