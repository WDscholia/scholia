#defaultView:Table

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?count
  ?author ?authorLabel ?authorDescription (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  (COALESCE(?orcid_, CONCAT("orcid-search/search/?searchQuery=", ?authorLabel)) AS ?orcid) 
WITH {
  SELECT
    ?author
    (count(?work) as ?count)
  WHERE {
    { ?work wdt:P921/wdt:P31*/wdt:P279* target: . }
    UNION
    { ?work wdt:P921/wdt:P361+ target: . }
    UNION
    { ?work wdt:P921/wdt:P1269+ target: . }
    ?work wdt:P50 ?author .
  }
  GROUP BY ?author
  ORDER BY DESC(?count)
  LIMIT 200
} AS %result
WHERE {
  INCLUDE %result
          
  # Include optional ORCID iD
  OPTIONAL { ?author wdt:P496 ?orcid_ . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . } 
}
ORDER BY DESC(?count)
