{% import 'sparql-helpers.sparql' as sparql_helpers -%}
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?date ?deadline ?deadlineLabel ?short_name ?event ?eventLabel (CONCAT("/event/", SUBSTR(STR(?event),32)) AS ?eventUrl) ?location ?locationLabel (CONCAT("/location/", SUBSTR(STR(?location),32)) AS ?locationUrl) {
  {
    SELECT ?datetime ?deadline ?event (SAMPLE(?location_) AS ?location) (SAMPLE(?short_name_) AS ?short_name) WHERE {
      ?event p:P793 ?key_event_statement .
      ?key_event_statement ps:P793 ?deadline ;
                           pq:P585 ?datetime .
      ?deadline wdt:P279* wd:Q2404808 .
      # Subtract a day from now to get events on the now date
      # FILTER (NOW() - "P1DT0H0M0.000S"^^xsd:duration < ?datetime)
      OPTIONAL {
        ?event wdt:P276 ?location_
      }
      OPTIONAL {
        ?event wdt:P1813 ?short_name_
      }
    }
    GROUP BY ?datetime ?deadline ?event
  }
  BIND (xsd:date(?datetime) AS ?date)
  {{ sparql_helpers.labels(["?event", "?deadline"], languages) }}
  {{ sparql_helpers.labels_unbound(["?location"], languages) }}
}
ORDER BY (?date)
