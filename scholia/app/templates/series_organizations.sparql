{% import 'sparql-helpers.sparql' as sparql_helpers -%}
#defaultView:BarChart
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?year ?count ?organization ?organizationLabel WHERE {
  {
    SELECT ?year (COUNT(?work) AS ?count) ?organization WHERE {
      {
        SELECT (MIN(?year_) AS ?year) ?work WHERE {
          ?collection wdt:P179 target: .
          ?work wdt:P1433 ?collection .
          ?work wdt:P577 ?datetimes .
          BIND (STR(YEAR(?datetimes)) AS ?year_)
        }
        GROUP BY ?work
      }
      ?work wdt:P50 ?author .
      # For now, only employment relation
      # We could add affiliation with "| wdt:P1416/wdt:P361*"
      # Note that this extra term will double count.
      ?author wdt:P108 ?organization .
    }
    GROUP BY ?year ?organization
    # Exclude organizations with only a single count
    # HAVING(?count > 1)
    }
  {{ sparql_helpers.labels(["?organization"], languages) }}
  }
 ORDER BY ?year
