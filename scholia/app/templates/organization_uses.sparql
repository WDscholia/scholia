PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?researchers
  ?use ?useLabel (CONCAT("/use/", SUBSTR(STR(?use), 32)) AS ?useUrl)
  ("ðŸ”Ž" AS ?zoom)
  (CONCAT("{{ q }}/use/", SUBSTR(STR(?use), 32)) AS ?zoomUrl)
  ?useDescription
  ?samplework ?sampleworkLabel (CONCAT("/work/", SUBSTR(STR(?samplework), 32)) AS ?sampleworkUrl)
WITH {
  SELECT DISTINCT ?researcher WHERE {
    ?researcher ( wdt:P108 | wdt:P463 | wdt:P1416 ) / wdt:P361* target: .
  } 
} AS %researchers
WITH {
  SELECT DISTINCT ?use
    (COUNT(DISTINCT ?researcher) AS ?researchers)
    (SAMPLE(?work) AS ?samplework)
  WHERE {
    INCLUDE %researchers
    ?work wdt:P50 ?researcher . 
    ?work wdt:P4510 ?use . 
  } 
  GROUP BY ?use
  ORDER BY DESC(?researchers)
  LIMIT 500
} AS %works_and_number_of_researchers
WHERE {
  INCLUDE %works_and_number_of_researchers
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,nl,no,ru,sv,zh" . } 
}
GROUP BY ?researchers ?use ?useLabel ?useDescription ?samplework ?sampleworkLabel
ORDER BY DESC(?researchers)
