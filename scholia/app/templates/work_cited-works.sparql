#defaultView:Table

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# List of works that is cited by the specified work
SELECT ?citations ?publication_date ?cited_work ?cited_workLabel
  (GROUP_CONCAT(DISTINCT ?intentionLabel;separator=", ") AS ?intentions)
WITH {
  SELECT (MIN(?date) AS ?publication_date) (COUNT(DISTINCT ?citing_cited_work) AS ?citations) ?cited_work 
  WHERE {
    target: wdt:P2860 ?cited_work .
    OPTIONAL {
      ?cited_work wdt:P577 ?datetime .
      BIND(xsd:date(?datetime) AS ?date)
    }
    OPTIONAL { ?citing_cited_work wdt:P2860 ?cited_work }
  }
  GROUP BY ?cited_work
} AS %result
WHERE {
  INCLUDE %result
  OPTIONAL {
    target: p:P2860 ?citationStatement .
    ?citationStatement pq:P3712 ?intention ;
                       ps:P2860 ?cited_work .
    ?intention rdfs:label ?intentionLabel . FILTER (lang(?intentionLabel) = "en")
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . } 
} GROUP BY ?citations ?publication_date ?cited_work ?cited_workLabel
ORDER BY DESC(?citations) DESC(?date)
