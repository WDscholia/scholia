{% import 'sparql-helpers.sparql' as sparql_helpers -%}

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT
  ?intention ?intentionLabel (CONCAT("/cito/", SUBSTR(STR(?intention), 32)) AS ?intentionUrl)
  (CONCAT("cito:", SUBSTR(STR(?cito), 27)) AS ?CiTO) (?cito AS ?CiTOUrl)
  (COUNT(DISTINCT ?citingArticle) AS ?count)
WHERE {
  ?citingArticle p:P2860 ?citationStatement ;
                 wdt:P1433 target: .
  ?citationStatement pq:P3712 ?intention ;
                     ps:P2860 ?citedArticle .
  ?intention wdt:P31 wd:Q96471816 ;
             wdt:P2888 ?cito .
  FILTER(STRSTARTS(STR(?cito), "http://purl.org/spar/cito/"))
  {{ sparql_helpers.labels(["?intention"], languages) }}
}
GROUP BY ?cito ?intention ?intentionLabel
ORDER BY DESC(?count)
