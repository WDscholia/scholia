PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?intention ?intentionLabel (CONCAT("/cito/", SUBSTR(STR(?intention), 32)) AS ?intentionUrl)
(COUNT(DISTINCT ?citingArticle) AS ?count) WHERE {
  VALUES ?JOURNAL { target: }
  ?citingArticle p:P2860 ?citationStatement ;
                 wdt:P1433 ?JOURNAL .
  ?citationStatement pq:P3712 ?intention ;
                     ps:P2860 ?citedArticle .
  ?intention wdt:P31 wd:Q96471816 ;
             wdt:P2888 ?cito .
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
} GROUP BY ?cito ?intention ?intentionLabel
  ORDER BY DESC(?count)
