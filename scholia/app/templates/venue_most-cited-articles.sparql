# title: Most cited articles published in specific venue
#defaultView:Table
SELECT
  ?count
  ?work ?workLabel
  ?example_citing_work ?example_citing_workLabel
WITH {
  # Count citations
  SELECT
    (COUNT(?citing_work) AS ?count)
    ?work
    (SAMPLE(?citing_work) AS ?example_citing_work)
  WHERE {
    ?work wdt:P1433 wd:{{ q }} .
    ?citing_work wdt:P2860 ?work.
  }
  GROUP BY ?work
  ORDER BY DESC(?count)
  LIMIT 1000  
} AS %result
WHERE {
  # Label results
  INCLUDE %result
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . } 
}
ORDER BY DESC(?count)
