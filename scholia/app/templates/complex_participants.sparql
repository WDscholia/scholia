SELECT ?quantity ?part ?partLabel ?partDescription ?type
WITH {
  SELECT ?part ?quantity (GROUP_CONCAT(DISTINCT ?type_label; separator=", ") AS ?type)
  WHERE {
    VALUES ?complex { wd:{{ q }} }
    ?complex p:P527 ?partStatement .
    ?partStatement ps:P527 ?part .
    OPTIONAL { ?partStatement pq:P1114 ?quantity }
    OPTIONAL { 
      ?part wdt:P31 ?type_ .
      ?type_ rdfs:label ?type_label .
      FILTER (LANG(?type_label) = 'en')
    }
  } GROUP BY ?part ?quantity 
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . }
} ORDER BY ASC(?partLabel)
