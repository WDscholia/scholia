#defaultView:Table

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?count ?short_name 
?venue ?venueLabel (CONCAT("/venue/", SUBSTR(STR(?venue), 32)) AS ?venueUrl)
WITH {
  SELECT (count(?work) as ?count) ?venue (SAMPLE(?short_name_) AS ?short_name) WHERE {
    { ?work wdt:P6104 target: . }
    union { ?work wdt:P5008 target: . }
    ?work wdt:P1433/wdt:P179* ?venue .
    OPTIONAL { ?venue wdt:P1813 ?short_name_ . }
  }
  GROUP BY ?venue
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . } 
}
ORDER BY DESC(?count)
LIMIT 200
