SELECT
  ?date
  ?work ?workLabel
  ?authors ?authorsUrl
  ?topics ?topicsUrl
WITH {
  SELECT
    (MIN(?date_) AS ?date) 
    ?work
    (GROUP_CONCAT(DISTINCT ?author_label; separator=", ") AS ?authors)
    (CONCAT("../authors/", GROUP_CONCAT(DISTINCT SUBSTR(STR(?author), 32); separator=",")) AS ?authorsUrl)
    (GROUP_CONCAT(DISTINCT ?topic_label; separator=", ") AS ?topics)
    (CONCAT("../topics/", GROUP_CONCAT(DISTINCT SUBSTR(STR(?topic), 32); separator=",")) AS ?topicsUrl)
  WHERE {
    ?work wdt:P1433 / wdt:P4745 / (wdt:P179 | wdt:P31) wd:{{ q }} .
    OPTIONAL {
      ?work wdt:P577 ?publication_datetime .
      BIND(xsd:date(?publication_datetime) AS ?date_)
    }
    OPTIONAL {
      ?work wdt:P50 ?author .
      ?author rdfs:label ?author_label . FILTER(LANG(?author_label) = '{{ g.lang_code }}')
    }
    OPTIONAL {
      ?work wdt:P921 ?topic .
      ?topic rdfs:label ?topic_label . FILTER(LANG(?topic_label) = '{{ g.lang_code }}')
    }
  }
  GROUP BY ?work
  ORDER BY DESC(?date)
  LIMIT 500
} AS %results
WHERE {
  INCLUDE %results
  SERVICE wikibase:label { bd:serviceParam wikibase:language "{{ g.lang_code }},[AUTO_LANGUAGE],en". }
}
ORDER BY DESC(?date)
