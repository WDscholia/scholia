PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT DISTINCT ?work (REPLACE(STR(?work), ".*/", "") AS ?workLabel) (CONCAT("/work/", SUBSTR(STR(?work),32)) AS ?workUrl) ?softwarename
  (CONCAT(SUBSTR(STR(?work),32), "\tP4510\t{{ q }}\tS887\tQ69652283") AS ?quickStatements)
(CONCAT("https://quickstatements.toolforge.org/#/v1=", 
          SUBSTR(STR(?work),32), "%7CP4510%7C{{ q }}%7CS887%7CQ69652283") AS ?quickStatementsUrl)
WHERE { {
  SELECT ?work ?softwarename WHERE {
    target: wdt:P31 wd:Q7397 ;
            rdfs:label ?softwarename .
    FILTER (LANG(?softwarename) = "en")
    SERVICE wikibase:mwapi {
      bd:serviceParam wikibase:endpoint "www.wikidata.org" ;
                      wikibase:api "Generator" ;
                      mwapi:generator "search" ;
                      mwapi:gsrsearch ?softwarename ;
                      mwapi:gsrlimit "max" .
      ?work wikibase:apiOutputItem mwapi:title.
    }
    ?work wdt:P1476 ?title .
    MINUS {
      ?work wdt:P4510 target:
    }
    FILTER (REGEX(LCASE(?title),LCASE(CONCAT("\\\\", "b", ?softwarename, "\\\\", "b"))))
  }
  LIMIT 200
}
# SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
}
