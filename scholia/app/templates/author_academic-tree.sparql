#defaultView:Graph
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
# Doctoral student/advisor network with a source from a spectific researcher
PREFIX gas: <http://www.bigdata.com/rdf/gas#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT DISTINCT ?student1 (REPLACE(STR(?student1), ".*/", "") AS ?student1Label) ?rgb ?supervisor1 (REPLACE(STR(?supervisor1), ".*/", "") AS ?supervisor1Label) WHERE {
  {
    {
      SELECT ?student1 ?supervisor1 WHERE {
        # Forward traversal: all reachable students and their predecessor (closest supervisor)
        # compute all reachable students
        target: wdt:P185+ ?student1 .
        # compute all of their supervisors
        ?supervisor1 wdt:P185 ?student1 .
        # ensure that the supervisor is a predecessor
        ?target  wdt:P185* ?supervisor1 .
      } GROUP BY ?student1 ?supervisor1
    } UNION {
        BIND (target: AS ?student1)
    }

# previous implementation using gas
#   SELECT ?student1 ?supervisor1 (MIN(?depth1) AS ?depth) WHERE {
#     SERVICE gas:service {
#       gas:program gas:gasClass "com.bigdata.rdf.graph.analytics.BFS" ;
#                   gas:in target: ;
#                   gas:traversalDirection "Forward" ;  # "Reverse" for reverse cases
#                   gas:out ?student1 ;
#                   gas:out1 ?depth1 ;
#                   gas:out2 ?supervisor1 ;
#                   gas:linkType wdt:P185 ;
#     }    

  }
  UNION {
    SELECT ?student1 ?supervisor1 WHERE {
      # Reverse traversal: all reachable supervisors and there direct predecessors
      # compute all reachable supervisors
      ?supervisor1 wdt:P185+ target: .
      # compute all of their students
      ?supervisor1 wdt:P185 ?student1 .
      # then make sure the student is a predecessor
      ?student1 wdt:P185* target: .
    } GROUP BY ?student1 ?supervisor1
  } UNION {
      BIND (target: AS ?supervisor1)
  }
  UNION {
    SELECT ?student1 ?supervisor1 WHERE {
      # Forward traversal: all reachable supervisors and their students
      # compute all reachable supervisors
      target: wdt:P184+ ?supervisor1 .
      # compute all of their students
      ?student1 wdt:P184 ?supervisor1 .
      # ensure that the student is a predecessor
      target:  wdt:P184* ?student1 .
    } GROUP BY ?student1 ?supervisor1
  }
  UNION {
    SELECT ?student1 ?supervisor1 WHERE {
      # Reverse traversal: all reachable students and their direct predecessors
      # compute all reachable students
      ?student1 wdt:P184+ target: .
      # compute all of their supervisors
      ?student1 wdt:P184 ?supervisor1 .
      # ensure the supervisor is a predecessor
      ?supervisor1 wdt:P184* target: .
    } GROUP BY ?student1 ?supervisor1
  }
  BIND (IF(?student1 = target:,"3080BB","ffffff") AS ?rgb)
  # SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .  } 
}
