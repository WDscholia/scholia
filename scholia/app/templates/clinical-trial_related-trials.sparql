PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT
  ?score
  ?trial ?trialLabel
WHERE {
  {
    SELECT
      (SUM(?score_) AS ?score)
      ?trial
    WHERE {
      BIND(target: AS ?query_trial)
      ?trial wdt:P31 wd:Q30612 .
      { 
        ?query_trial wdt:P1050 / ^wdt:P1050 ?trial .
        BIND(3 AS ?score_)
      }
      UNION 
      {
        ?query_trial wdt:P4844 / ^wdt:P4844 ?trial .
        BIND(2 AS ?score_)        
      }
      UNION 
      {
        ?query_trial wdt:P859 / ^wdt:P859 ?trial .
        BIND(1 AS ?score_)        
      }
    }
    GROUP BY ?trial
    ORDER BY DESC(?score)
    LIMIT 500
  }
  FILTER (target: != ?trial)
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?score)
