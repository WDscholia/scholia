{% import 'sparql-helpers.sparql' as sparql_helpers -%}
# title: recent literature about this chemical element
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?date ?work ?workLabel ?type ?topics WHERE {
  {
    SELECT (MAX(?dates) AS ?datetime) ?work (GROUP_CONCAT(DISTINCT ?type_label; SEPARATOR=", ") AS ?type) (GROUP_CONCAT(DISTINCT ?topic_label; SEPARATOR=" // ") AS ?topics) WHERE {
      {
        SELECT DISTINCT ?work WHERE {
          {
            ?work wdt:P921/(wdt:P527+ | wdt:P1269+ | (wdt:P31*/wdt:P279*)) target: .
          }
          UNION {
            target: ?propp ?statement .
            ?statement a wikibase:BestRank ;
                       prov:wasDerivedFrom/pr:P248 ?work .
          }
        }
      }
      ?work wdt:P921 ?topic .
      OPTIONAL {
        ?work wdt:P31 ?type_ .
        ?type_ rdfs:label ?type_label . FILTER (LANG(?type_label) = 'en')
      }
      OPTIONAL {
        ?work wdt:P577 ?dates .
      }
      ?topic rdfs:label ?topic_label . FILTER (LANG(?topic_label) = 'en')
    }
    GROUP BY ?work
  }
  BIND (xsd:date(?datetime) AS ?date)
  {{ sparql_helpers.labels(["?work"], languages) }}
}
ORDER BY DESC(?date)
LIMIT 500
