SELECT ?count ?protein ?proteinLabel ?species ?speciesLabel ?functions
WITH {
  SELECT ?protein (COUNT(?function) AS ?count)
         (GROUP_CONCAT(?function_label; separator=" // ") AS ?functions)
  WHERE {
    BIND(wd:{{ q }} AS ?query_protein)
    ?query_protein wdt:P680 ?function .
    FILTER (?query_protein != ?protein)
    ?protein wdt:P680 ?function . 
    ?function rdfs:label ?function_label FILTER(lang(?function_label) = "en")
  }
  GROUP BY ?protein
} AS %result 
WHERE {
  INCLUDE %result
  OPTIONAL { ?protein wdt:P703 ?species . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . }
}
ORDER BY DESC(?count)
LIMIT 500