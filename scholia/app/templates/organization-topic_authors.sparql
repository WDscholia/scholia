PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>

SELECT
  ?works
  ?author ?authorLabel ?authorDescription (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  SELECT
    (COUNT(DISTINCT ?work) AS ?works)
    ?author
    (SAMPLE(?work) AS ?example_work)
  {
    { ?author ?p1 target1: . }
    UNION
    {
      ?author ?p2 ?something .
      ?something wdt:P361 target1: .
    }
    ?work wdt:P50 ?author;
          wdt:P921 / wdt:P279* target2: .
  }
  GROUP BY ?author
  ORDER BY DESC (?works)
  LIMIT 200
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
ORDER BY DESC (?works)
