{% import 'sparql-helpers.sparql' as sparql_helpers -%}
PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>
# title: Nearby researchers that work with a specific topic and near a specified geographical entity
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?score ?author ?authorLabel (CONCAT("/author/", SUBSTR(STR(?author),32)) AS ?authorUrl) ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work),32)) AS ?example_workUrl) WHERE {
  {
    SELECT ?author (SUM(?topic_score) * MAX(?inverse_distance) AS ?score) (SAMPLE(?work) AS ?example_work) WHERE {
      { # Compute distance for authors
        SELECT (MAX(?inverse_distance_) AS ?inverse_distance) ?author WHERE {
          { # Academic institutions on all levels
            SELECT DISTINCT ?organization ?other_geo ?distance WHERE {
              { # Universities, research centers, etc.
                SELECT ?university WHERE {
                  VALUES ?university { wd:Q3918 wd:Q1371037 wd:Q7315155 wd:Q31855 wd:Q2385804 }
                }
              }
              {
                SELECT ?organization ?distance ?other_geo {
                  target1: wdt:P625 ?geo .
                  SERVICE wikibase:around {
                    ?organization wdt:P625 ?other_geo .
                    bd:serviceParam wikibase:center ?geo .
                    bd:serviceParam wikibase:radius "50" .
                    bd:serviceParam wikibase:distance ?distance .
                  }
                }
              }
              ?organization wdt:P361*/wdt:P31/wdt:P279* ?university .
            }
          }
          BIND (1 / (?distance + 1) AS ?inverse_distance_)
          ?author wdt:P108 | wdt:P1416 ?organization .
        }
        GROUP BY ?author
      }
      {
        # A score of 3 for each authorship in the topic
        ?work wdt:P50 ?author .
        FILTER EXISTS {
          ?work wdt:P921/wdt:P279* target2: .
        }
        BIND (3 AS ?topic_score)
      }
      UNION {
        ?author wdt:P101 target2: .
        BIND (20 AS ?topic_score)
      }
    }
    GROUP BY ?author
  }
  {{ sparql_helpers.labels(["?example_work", "?author"], languages) }}
}
ORDER BY DESC(?score)
