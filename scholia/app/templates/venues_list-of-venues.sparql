SELECT ?venue ?venueLabel ?publisher ?publisherLabel ?number_of_works ?number_of_citations
WITH {
  SELECT ?venue (COUNT(DISTINCT ?work) AS ?number_of_works) (COUNT(?citing_work) AS ?number_of_citations)
  WHERE {
    VALUES ?venue {  {% for q in qs %} wd:{{ q }} {% endfor %}  }
    OPTIONAL {
      ?work wdt:P1433 ?venue .
      OPTIONAL { ?citing_work wdt:P2860 ?work }
    }
  } 
  GROUP BY ?venue
} AS %result
WHERE {
  INCLUDE %result
  OPTIONAL { ?venue wdt:P123 ?publisher }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "{{ g.lang_code }},[AUTO_LANGUAGE],en" . }
}
ORDER BY ?venueLabel