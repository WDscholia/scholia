#defaultView:Table
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT (MIN(?dates) AS ?date) ?work ?workLabel (CONCAT("../retraction/", SUBSTR(STR(?work),32)) AS ?workUrl) (GROUP_CONCAT(DISTINCT ?type_label; SEPARATOR=", ") AS ?type) (SAMPLE(?pages_) AS ?pages) ?venue ?venueLabel (GROUP_CONCAT(DISTINCT ?author_label; SEPARATOR=", ") AS ?authors) (CONCAT("../authors/", GROUP_CONCAT(DISTINCT SUBSTR(STR(?author),32); SEPARATOR=",")) AS ?authorsUrl) WHERE {
  ?work wdt:P50 target: .
  ?work wdt:P50 ?author .
  {
    ?work wdt:P31 wd:Q45182324
  }
  UNION {
    ?work wdt:P793 wd:Q7316896 .
  }
  UNION {
    ?work wdt:P5824 [] .
  }
  OPTIONAL {
    ?author rdfs:label ?author_label_ . FILTER (LANG(?author_label_) = 'en')
  }
  BIND (COALESCE(?author_label_, SUBSTR(STR(?author),32)) AS ?author_label)
  OPTIONAL {
    ?work wdt:P31 ?type_ .
    ?type_ rdfs:label ?type_label . FILTER (LANG(?type_label) = 'en')
  }
  ?work wdt:P577 ?datetimes .
  BIND (xsd:date(?datetimes) AS ?dates)
  OPTIONAL {
    ?work wdt:P1104 ?pages_
  }
  OPTIONAL {
    ?work wdt:P1433 ?venue
  }
  # SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
GROUP BY ?work ?workLabel ?workUrl ?venue ?venueLabel
ORDER BY DESC(?date)
