SELECT
  ?year
  ?recipient ?recipientLabel ?recipientDescription
  ?example_work ?example_workLabel
WITH {
  SELECT DISTINCT ?recipient ?year (SAMPLE(?work) AS ?example_work) WHERE {
    ?recipient p:P166 ?award_statement . 
    ?award_statement ps:P166 wd:{{ q }} .
    OPTIONAL {
      { ?award_statement pq:P585 ?time }
      UNION
      { ?award_statement pq:P580 ?time }
      BIND(YEAR(?time) AS ?year)
    }
    OPTIONAL { ?work wdt:P50 ?recipient . }
  }
  GROUP BY ?recipient ?year
} AS %result
WHERE {
  INCLUDE %result 
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en,da,de,es,fr,jp,no,ru,sv,zh" . }  
}
ORDER BY DESC(?year)
