# title: List of works on any combination of specific uses
SELECT
  ?count 
  (MIN(?publication_date_) AS ?publication_date)
  ?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work), 32)) AS ?workUrl)
  ?uses ?usesUrl
WITH {
  SELECT (COUNT(?use) AS ?count) ?work
    (GROUP_CONCAT(DISTINCT ?use_label; separator=" // ") AS ?uses) 
    (CONCAT("../uses/", GROUP_CONCAT(DISTINCT SUBSTR(STR(?use), 32); separator=",")) AS ?usesUrl)                                    
  WHERE {
    VALUES ?use { {% for q in qs %} wd:{{ q }} {% endfor %} }
    
    # A broad query including "(wdt:P361+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) )" seems to take
    # long time and time out.
    ?work wdt:P4510 ?use  .
    ?use rdfs:label ?use_label .
    FILTER(LANG(?use_label) = 'en')
  }
  GROUP BY ?work ?uses
  HAVING(?count > 1)
  ORDER BY DESC(?count)
  LIMIT 200
} AS %results
WHERE {
  INCLUDE %results
  OPTIONAL {
    ?work wdt:P577 ?publication_datetime .
    BIND(xsd:date(?publication_datetime) AS ?publication_date_)
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
GROUP BY ?count ?work ?workLabel ?uses ?usesUrl
ORDER BY DESC(?count) DESC(?publication_date)
