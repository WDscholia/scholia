#defaultView:Table

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?count ?short_name 
?venue ?venueLabel (CONCAT("/venue/", SUBSTR(STR(?venue), 32)) AS ?venueUrl)
WITH {
  SELECT (count(?work) as ?count) ?venue (SAMPLE(?short_name_) AS ?short_name) WHERE {
    { ?work wdt:P921/wdt:P31*/wdt:P279* target: . }
    union { ?work wdt:P921/wdt:P361+ target: . }
    union { ?work wdt:P921/wdt:P1269+ target: . }
    ?work wdt:P1433/wdt:P179* ?venue .
    OPTIONAL { ?venue wdt:P1813 ?short_name_ . }
  }
  GROUP BY ?venue
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . } 
}
ORDER BY DESC(?count)
LIMIT 200
