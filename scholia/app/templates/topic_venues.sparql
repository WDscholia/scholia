#defaultView:Table
SELECT ?count ?short_name ?venue ?venueLabel
WITH {
  SELECT (count(?work) as ?count) ?venue (SAMPLE(?short_name_) AS ?short_name) WHERE {
    { ?work wdt:P921/wdt:P31*/wdt:P279* wd:{{ q }} . }
    union { ?work wdt:P921/wdt:P361+ wd:{{ q }} . }
    union { ?work wdt:P921/wdt:P1269+ wd:{{ q }} . }
    ?work wdt:P1433/wdt:P179* ?venue .
    OPTIONAL { ?venue wdt:P1813 ?short_name_ . }
  }
  GROUP BY ?venue
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "{{ g.lang_code }},[AUTO_LANGUAGE],en,da,de,es,fr,jp,nl,no,ru,sv,zh" . } 
}
ORDER BY DESC(?count)
LIMIT 200
