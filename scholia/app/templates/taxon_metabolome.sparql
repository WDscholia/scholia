PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT DISTINCT
  ?metabolite ?metaboliteLabel ?metaboliteDescription
WITH {
  SELECT DISTINCT ?children WHERE {
      ?children (wdt:P171*) target:.
  }
} AS %taxa 
WITH {
  SELECT DISTINCT ?metabolite WHERE {
    INCLUDE %taxa
            { ?metabolite wdt:P703 ?children }
    UNION
    { ?metabolite wdt:P1582 ?children }
    VALUES ?chemical { wd:Q11173 wd:Q59199015 wd:Q56256086 wd:Q15711994 }.
    ?metabolite wdt:P31|wdt:P279/wdt:P31|wdt:P279 ?chemical .
  }
} AS %results {
  INCLUDE %results
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
