PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT
  ?number_of_works
  ?author ?authorLabel (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)

WHERE {
  { SELECT
    ?author
    (COUNT(DISTINCT ?work) AS ?number_of_works)
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    { SELECT DISTINCT ?author WHERE {
    ?author ( wdt:P108 | wdt:P1416 ) / wdt:P17 target1: .
  } }
    ?work wdt:P50 ?author .
    ?work wdt:P921 / (wdt:P361+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) ) target2: .
  }
  GROUP BY ?author }
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?number_of_works)
