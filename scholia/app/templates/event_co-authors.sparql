{% import 'sparql-helpers.sparql' as sparql_helpers -%}
#defaultView:Graph
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?author1 ?author1Label (SAMPLE(?image1_) AS ?image1) ?rgb ?author2 ?author2Label (SAMPLE(?image2_) AS ?image2) WHERE {
  {
    SELECT DISTINCT ?author1 ?author2 ?rgb WHERE {
     { SELECT ?author1 ?rgb ?work WHERE {
       {
        SELECT ?author1 (SAMPLE(?dark_rgb_) AS ?dark_rgb) (SAMPLE(?light_rgb_) AS ?light_rgb) WHERE {
          # Find people at the event
          {
            target: (wdt:P823 | wdt:P664 | ^wdt:P1344 | wdt:P710) ?author1 .
            BIND ("EEEEEE" AS ?dark_rgb_)
          }
          UNION {
            # author of article in proceedings, or program committee member
            target: (^wdt:P4745/^wdt:P1433/wdt:P50) | wdt:P5804 ?author1 .
            BIND ("FFFFFF" AS ?light_rgb_)
          }
          UNION {
            # author of article in proceedings, or program committee member
            ?presentation wdt:P823 ?author1 ;
                          wdt:P5072 target:
            BIND ("FFFFFF" AS ?light_rgb_)
          }
        }
        GROUP BY ?author1
       }
         BIND (COALESCE(?dark_rgb, ?light_rgb) AS ?rgb)
 	 ?work wdt:P50 ?author1 .
       }
      }
      {
        target: (wdt:P823 | wdt:P664 | ^wdt:P1344 | wdt:P710 | ^wdt:P4745/^wdt:P1433/wdt:P50) | wdt:P5804 ?author2 .
      }
      UNION {
        ?presentation wdt:P823 ?author2 ;
                      wdt:P5072 target:
      }
      # Find co-authors
      ?work wdt:P50 ?author2 .
      FILTER (?author1 != ?author2)
    }
  }
  OPTIONAL {
    ?author1 wdt:P18 ?image1_ .
  }
  OPTIONAL {
    ?author2 wdt:P18 ?image2_ .
  }
  OPTIONAL { ?author2 <http://www.w3.org/2000/01/rdf-schema#label> ?author2Label. FILTER(LANG(?author2Label) = "en-US") }
    OPTIONAL { ?author2 <http://www.w3.org/2000/01/rdf-schema#label> ?author2Label. FILTER(LANG(?author2Label) = "en") }
    OPTIONAL { ?author2 <http://www.w3.org/2000/01/rdf-schema#label> ?author2Label. FILTER(LANG(?author2Label) = "mul") }
    
    OPTIONAL { ?author1 <http://www.w3.org/2000/01/rdf-schema#label> ?author1Label. FILTER(LANG(?author1Label) = "en-US") }
    OPTIONAL { ?author1 <http://www.w3.org/2000/01/rdf-schema#label> ?author1Label. FILTER(LANG(?author1Label) = "en") }
    OPTIONAL { ?author1 <http://www.w3.org/2000/01/rdf-schema#label> ?author1Label. FILTER(LANG(?author1Label) = "mul") }
    
} GROUP BY
?author1
?author1Label
?rgb
?author2
?author2Label
