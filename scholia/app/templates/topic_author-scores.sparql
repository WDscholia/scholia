PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT ?score 
?author ?authorLabel (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
 
WHERE {
  { SELECT
    (SUM(?score_) AS ?score)
    ?author
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    { 
      ?author wdt:P101/wdt:P279* target: .
      BIND(20 AS ?score_)
    }
    UNION
    {
      SELECT (3 AS ?score_) ?author ?work WHERE {
        ?work wdt:P50 ?author ;
              wdt:P921/wdt:P279* target: .
      }
    }
    UNION
    {
      SELECT (1 AS ?score_) ?author ?work WHERE {
        ?work wdt:P50 ?author .
        ?citing_work wdt:P2860 ?work .
        ?citing_work wdt:P921/wdt:P279* target: .
      }
    }
  }
  GROUP BY ?author
  ORDER BY DESC(?score)
  LIMIT 200 }
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,da,de,es,jp,no,ru,sv,zh". }
}
ORDER BY DESC(?score)
