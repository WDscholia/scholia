# tool: scholia
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?number_of_topics
  ?citing_works
  ?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work), 32)) AS ?workUrl)
WITH {
  # Count citations
  SELECT
    (COUNT(DISTINCT ?citing_work) AS ?citing_works)
    ?work
    (SAMPLE(?citing_work) AS ?example_citing_work)
    (COUNT(DISTINCT ?topic) AS ?number_of_topics)
  WHERE {
    # subclasses included to get version of resources
    ?work wdt:P4510 / wdt:P279* target: .
    
    OPTIONAL { ?work wdt:P921 ?topic . }
    OPTIONAL { ?citing_work wdt:P2860 ?work. }
  }
  GROUP BY ?work
  # HAVING (?topics < 1)
  ORDER BY DESC(?count)
  LIMIT 200
} AS %result
WHERE {
  # Label results
  INCLUDE %result
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . } 
}
ORDER BY ?number_of_topics DESC(?citing_works)
