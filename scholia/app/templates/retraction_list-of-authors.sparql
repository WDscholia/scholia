{% import 'sparql-helpers.sparql' as sparql_helpers -%}

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
# List of authors for a work
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?order ?author ?authorLabel ?authorUrl ?retractions WHERE {
  {
    { SELECT ?author (COUNT(DISTINCT ?work) AS ?retractions) WHERE {
      target: wdt:P50 ?author .
      ?work wdt:P50 ?author ;
            wdt:P31 wd:Q45182324 .
    } GROUP BY ?author }

    target: p:P50 ?authorStatement .
    ?authorStatement ps:P50 ?author .
    BIND (CONCAT("../author/", SUBSTR(STR(?author), 32), "#list-of-retracted-articles") AS ?authorUrl)
    {{ sparql_helpers.labels(["?author"], languages) }}
  } UNION {
    target: p:P2093 ?authorStatement .
    ?authorStatement ps:P2093 ?author .
    BIND (CONCAT(?author, " â†—") AS ?authorLabel)
    BIND (CONCAT("https://author-disambiguator.toolforge.org/names_oauth.php?doit=Look+for+author&name=", ENCODE_FOR_URI(?author)) AS ?authorUrl)
  }

  OPTIONAL { ?authorStatement pq:P1545 ?_order . }
  BIND (xsd:integer(?_order) AS ?order)
} ORDER BY ?order
