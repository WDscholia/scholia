PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?number_of_roles
# (?number_of_publications AS ?works)
?person ?personLabel ?personDescription (CONCAT("/author/", SUBSTR(STR(?person),32)) AS ?personUrl) ?roles ?example_work ?example_workLabel WHERE {
  {
    SELECT ?number_of_roles
    # (COUNT(?work) AS ?number_of_publications)
    ?roles ?person (SAMPLE(?work) AS ?example_work) WHERE {
      {
        SELECT (COUNT(?role) AS ?number_of_roles) (GROUP_CONCAT(DISTINCT ?role; SEPARATOR=", ") AS ?roles) ?person WHERE {
          {
            # speaker
            target: ^wdt:P179/wdt:P823 ?person .
            BIND ("speaker" AS ?role)
          }
          UNION {
            # organizer
            target: ^wdt:P179/wdt:P664 ?person .
            BIND ("organizer" AS ?role)
          }
          UNION {
            # participant
            ?person (wdt:P1344 | ^wdt:P710)/wdt:P179 target: .
            BIND ("participant" AS ?role)
          }
          UNION {
            # editor
            ?person (^wdt:P98/wdt:P4745)/wdt:P179 target: .
            BIND ("editor of proceedings" AS ?role)
          }
          UNION {
            # author
            ?person (^wdt:P50/wdt:P1433/wdt:P4745)/wdt:P179 target: .
            BIND ("author" AS ?role)
          }
          UNION {
            # program committee member
            target: ^wdt:P179/wdt:P5804 ?person .
            BIND ("program committee member" AS ?role)
          }
        }
        GROUP BY ?person
      } .
      OPTIONAL {
        ?work wdt:P50 ?person .
      }
    }
    GROUP BY ?number_of_roles ?roles ?person
  }
  # SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,da,de,es,fr,jp,no,ru,sv,zh". }
}
ORDER BY DESC(?number_of_roles)
LIMIT 500
