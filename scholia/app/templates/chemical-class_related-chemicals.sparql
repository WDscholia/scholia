# title: lists related chemicals for this class
#sparql_endpoint = https://query.wikidata.org/sparql
#sparql_endpoint_name= WDQS&nbsp;main
#sparql_editurl = https://query.wikidata.org/#
#sparql_embedurl = https://query.wikidata.org/embed.html#
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?mol (REPLACE(STR(?mol), ".*/", "") AS ?molLabel) ?InChIKey ?CAS ?CASUrl ?ChemSpider ?ChemSpiderUrl ?PubChem_CID ?PubChem_CIDUrl WHERE {
  {
    SELECT DISTINCT ?mol WHERE {
      ?mol wdt:P279* target: .
      MINUS {
        [] wdt:P279 ?mol
      }
    }
    LIMIT 500
  }
  OPTIONAL {
    ?mol wdt:P235 ?InChIKey
  }
  OPTIONAL {
    VALUES ?CASIDdir { wdt:P231 }
    ?mol ?CASIDdir ?CAS .
    OPTIONAL {
      ?CASIDpred wikibase:directClaim ?CASIDdir .
      ?CASIDpred wdt:P1630 ?CASformatterurl .
    }
    BIND (IRI(REPLACE(?CASformatterurl, '\\\\$1', STR(?CAS))) AS ?CASUrl) .
  }
  OPTIONAL {
    VALUES ?IDdir { wdt:P661 }
    ?mol ?IDdir ?ChemSpider .
    OPTIONAL {
      ?IDpred wikibase:directClaim ?IDdir .
      ?IDpred wdt:P1630 ?formatterurl .
    }
    BIND (IRI(REPLACE(?formatterurl, '\\\\$1', STR(?ChemSpider))) AS ?ChemSpiderUrl) .
  }
  OPTIONAL {
    ?mol wdt:P662 ?PubChem_CID
  }
  OPTIONAL {
    VALUES ?PCIDdir { wdt:P662 }
    ?mol ?PCIDdir ?PubChem_CID .
    OPTIONAL {
      ?PCIDpred wikibase:directClaim ?PCIDdir .
      ?PCIDpred wdt:P1630 ?PCformatterurl .
    }
    BIND (IRI(REPLACE(?PCformatterurl, '\\\\$1', STR(?PubChem_CID))) AS ?PubChem_CIDUrl) .
  }
  # SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
