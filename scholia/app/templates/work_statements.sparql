#defaultView:Table

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# Statements referencing this article
SELECT ?item ?itemLabel ?property ?propertyLabel ?value ?valueLabel ?unit ?unitLabel
WITH {
  SELECT ?statement WHERE { 
	  ?statement prov:wasDerivedFrom/pr:P248 target: .
  }
  LIMIT 2000
} AS %statements 
WITH {
  SELECT distinct ?item ?property ?value ?unit
  WHERE {
    INCLUDE %statements
    ?item ?p ?statement .
    ?property wikibase:claim ?p . 
    ?statement ?a ?value .
    FILTER (!CONTAINS(LCASE(STR(?a)), "http://www.wikidata.org/prop/qualifier/"))
    ?item ?b ?value . 
    OPTIONAL {?statement ?psv_statement_predicate ?psv_statement .
    ?statement_predicate_property wikibase:statementValue ?psv_statement_predicate.
    ?psv_statement wikibase:quantityUnit ?unit}
  }
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,it,jp,nl,no,ru,sv,zh" . } 
} 
ORDER BY DESC(?itemLabel)
