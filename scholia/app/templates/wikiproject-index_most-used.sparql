SELECT 
  ?count
  ?wikiproject ?wikiprojectLabel
  (CONCAT("/wikiproject/", SUBSTR(STR(?wikiproject), 32)) AS ?wikiprojectUrl)
  ?wikiprojectDescription
  ?example ?exampleLabel (CONCAT("/", SUBSTR(STR(?example), 32)) AS ?exampleUrl)
WITH {
  SELECT 
    (COUNT(?item) AS ?count)
    ?wikiproject
    (SAMPLE(?item) AS ?example)
  WHERE {
    ?wikiproject wdt:P31 wd:Q16695773 ;
                 ^(wdt:P6104 | wdt:P5008) ?item .
  }
  GROUP BY ?wikiproject
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
ORDER BY DESC(?count)
LIMIT 1000