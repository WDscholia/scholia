#defaultView:Map

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?location ?locationLabel
  ?geo
  ?example_work ?example_workLabel
WITH {
  SELECT
    ?location ?geo
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    # Find works that are marked with main subject of the topic.
    ?work wdt:P921 / ( wdt:P31*/wdt:P279* | wdt:P361+ | wdt:P1269+ ) target: .
    
    # Identify co-occuring topic that is geo-locatable. 
    ?work wdt:P921 ?location .
    ?location wdt:P625 ?geo .
  }
  GROUP BY ?location ?geo
} AS %results
WHERE {
  INCLUDE %results
  
  # Label the results
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" .
  }
}
