PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT
  ?count
  ?author ?authorLabel ?authorDescription
  ?example_work ?example_workLabel

WHERE {
  { SELECT
    (COUNT(?work) AS ?count)
    ?author
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    ?work (wdt:P2283 | wdt:P4510) / wdt:P279* target: ;
          wdt:P50 ?author .
  }
  GROUP BY ?author
  ORDER BY DESC(?count)
  LIMIT 500 }
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?count)
