PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?count
  ?author ?authorLabel ?authorDescription
  ?example_work ?example_workLabel
WITH{
  SELECT
    (COUNT(?work) AS ?count)
    ?author
    (SAMPLE(?work) AS ?example_work)
  {
    ?work (wdt:P2283 | wdt:P4510) / wdt:P279* target: ;
          wdt:P50 ?author .
  }
  GROUP BY ?author
  ORDER BY DESC(?count)
  LIMIT 500
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,nl,no,ru,sv,zh" . }
}
ORDER BY DESC(?count)
LIMIT 200
