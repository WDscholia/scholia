# title: Authors for specific uses
SELECT
  ?uses ?works
  ?author ?authorLabel (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  SELECT
    (COUNT(DISTINCT ?use) AS ?uses)
    (COUNT(DISTINCT ?work) AS ?works)
    ?author (SAMPLE(?work) AS ?example_work)
  WHERE {
    VALUES ?use { {% for q in qs %} wd:{{ q }} {% endfor %} }
    
    ?work wdt:P4510 / wdt:P279* ?use ;
          wdt:P50 ?author .
  }
  GROUP BY ?author
  ORDER BY DESC(?uses)
  LIMIT 200
} AS %results
WHERE {
  INCLUDE %results
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?uses) DESC(?works)