#defaultView:BarChart
# Inspired from LEGOLAS - http://abel.lis.illinois.edu/legolas/
# Shubhanshu Mishra, Vetle Torvik
select ?year (count(?work) as ?number_of_publications) ?intention where {
  {
    select (str(?year_) as ?year) (0 as ?pages) ("_" as ?intention) where {
      # default values = 0
      ?year_item wdt:P31 wd:Q577 . 
      ?year_item wdt:P585 ?date .
      bind(year(?date) as ?year_)
      {
        select (min(?year_) as ?earliest_year)  (max(?year_) as ?latest_year) where {
          VALUES ?JOURNAL { wd:{{ q }} }
          ?work wdt:P1433 ?JOURNAL .
          ?work wdt:P577 ?publication_date . 
          bind(year(?publication_date) as ?year_)
        }
      }
      bind(year(now())+1 as ?next_year)
      filter (?year_ >= ?earliest_year && ?year_ <= ?latest_year)
    }
  }
  union {
    select ?work ?intention (min(?years) as ?year) where {
      VALUES ?JOURNAL { wd:{{ q }} }
      ?work wdt:P577 ?dates ;
            p:P2860 / pq:P3712 / rdfs:label ?intention ;
            wdt:P1433 ?JOURNAL .
      bind(str(year(?dates)) as ?years) .
      FILTER (LANG(?intention) = "en")
    }
    group by ?work ?intention
  }
}
group by ?year ?intention
order by ?year
