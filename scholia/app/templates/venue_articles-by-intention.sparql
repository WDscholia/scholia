#defaultView:BarChart
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
# Inspired from LEGOLAS - http://abel.lis.illinois.edu/legolas/
# Shubhanshu Mishra, Vetle Torvik
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT ?year (COUNT(?work) AS ?number_of_publications) ?intention WHERE {
  {
    SELECT (STR(?year_) AS ?year) (0 AS ?pages) ("_" AS ?intention) WHERE {
      # default values = 0
      ?year_item wdt:P31 wd:Q577 .
      ?year_item wdt:P585 ?date .
      BIND (YEAR(?date) AS ?year_)
      {
        SELECT (MIN(?year_) AS ?earliest_year) (MAX(?year_) AS ?latest_year) WHERE {
          VALUES ?JOURNAL { target: }
          ?work wdt:P1433 ?JOURNAL .
          ?work wdt:P577 ?publication_date .
          BIND (YEAR(?publication_date) AS ?year_)
        }
      }
      BIND (YEAR(NOW() 
    ) + 1 AS ?next_year ) FILTER ( ?year_ >= ?earliest_year && ?year_ <= ?latest_year )}
  }
  UNION {
    SELECT ?work ?intention (MIN(?years) AS ?year) WHERE {
      VALUES ?JOURNAL { target: }
      ?work wdt:P577 ?dates ;
            p:P2860/pq:P3712/rdfs:label ?intention ;
            wdt:P1433 ?JOURNAL .
      BIND (STR(YEAR(?dates)) AS ?years) .
      FILTER (LANG(?intention) = "en")
    }
    GROUP BY ?work ?intention
  }
}
GROUP BY ?year ?intention
ORDER BY ?year
