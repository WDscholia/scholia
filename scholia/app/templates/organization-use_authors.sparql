PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>

SELECT
  ?works
  ?author ?authorLabel ?authorDescription (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  SELECT DISTINCT ?author WHERE {
    ?author ( wdt:P108 | wdt:P463 | wdt:P1416 ) / wdt:P361* target1: .
  } 
} AS %researchers
WITH {
  SELECT
    ?author
    (COUNT(DISTINCT ?work) AS ?works)
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    INCLUDE %researchers
    ?work wdt:P50 ?author ;
          wdt:P4510 target2: .
  }
  GROUP BY ?author
} AS %researchers_and_works
WHERE {
  INCLUDE %researchers_and_works

  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en,da,de,es,fr,jp,nl,no,ru,sv,zh". }
}
ORDER BY DESC (?works)
