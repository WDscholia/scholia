{% import 'sparql-helpers.sparql' as sparql_helpers -%}
PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT ?works ?author ?authorLabel ?authorDescription (CONCAT("/author/", SUBSTR(STR(?author),32)) AS ?authorUrl) ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work),32)) AS ?example_workUrl) WHERE {
  {
    SELECT ?author (COUNT(DISTINCT ?work) AS ?works) (SAMPLE(?work) AS ?example_work) WHERE {
      {
        SELECT DISTINCT ?author WHERE {
          ?author (wdt:P108 | wdt:P463 | wdt:P1416)/wdt:P361* target1: .
        }
      }
      ?work wdt:P50 ?author ;
            wdt:P4510 target2: .
    }
    GROUP BY ?author
  }
  {{ sparql_helpers.labels(["?example_work", "?author"], languages) }}
  {{ sparql_helpers.descriptions(["?author"], languages) }}
}
ORDER BY DESC(?works)
