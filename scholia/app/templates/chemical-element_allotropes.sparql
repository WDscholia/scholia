{% import 'sparql-helpers.sparql' as sparql_helpers -%}
# title: list known allotropes
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT DISTINCT ?allotrope ?allotropeLabel ?density ?densityUnit ?densityUnitLabel {
  {
    SELECT ?allotrope (SAMPLE(?densityStats) AS ?densityStat) WHERE {
      ?allotrope wdt:P31 | wdt:P279 ?allotropeType .
      ?allotropeType p:P279 ?allotropeSpecsStatement .
      ?allotropeSpecsStatement ps:P279 wd:Q21198401 ;
                               pq:P642 target: .
      OPTIONAL {
        ?allotrope p:P2054 ?densityStats .
      }
    }
    GROUP BY ?allotrope
  }
  OPTIONAL {
    ?allotrope p:P2054 ?densityStat .
    ?densityStat psv:P2054 ?densityValNode .
    ?densityValNode wikibase:quantityAmount ?density ;
                    wikibase:quantityUnit ?densityUnit .
  }
  {{ sparql_helpers.labels(["?allotrope", "?densityUnit"], languages) }}
}
ORDER BY ASC(?allotropeLabel)
