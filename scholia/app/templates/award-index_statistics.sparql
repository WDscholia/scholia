#defaultView:ScatterChart
# Statistics on number of recipients for award wrt. gender
SELECT ?males ?females ?award ?awardLabel 
WITH {
  SELECT (COUNT(?recipient_male) AS ?males) (COUNT(?recipient_female) AS ?females) ?award WHERE {
    ?award wdt:P31 wd:Q11448906 .
    { 
      ?recipient_male wdt:P166 ?award .
      ?recipient_male wdt:P21 wd:Q6581097 .
    }
    UNION 
    {
      ?recipient_female wdt:P166 ?award .
      ?recipient_female wdt:P21 wd:Q6581072 .
    }  
  }
  GROUP BY ?award
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en,da,de,es,fr,jp,nl,no,ru,sv,zh". }
} 
ORDER BY DESC(?males) DESC(?female)