SELECT ?date ?work ?workLabel ?type ?topics
WITH {
  SELECT DISTINCT ?work WHERE {
    {
      ?work wdt:P921 / (wdt:P527+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) ) wd:{{ q }} .
    }
    UNION
    {
      # Works funded by the project, - recursively
      ?work wdt:P859+ wd:{{ q }} .
    }
    UNION
    {
      # Works funded by the project, - recursively
      ?work wdt:P859/(wdt:P1344|^wdt:P710)* wd:{{ q }} .
    }
    UNION {
      wd:{{ q }} ?propp ?statement .
      ?statement a wikibase:BestRank ;
                 prov:wasDerivedFrom/pr:P248 ?work .
    }
  }
} AS %works
WITH {
  SELECT
    (MAX(?dates) as ?datetime)
    ?work
    (GROUP_CONCAT(DISTINCT ?type_label; separator=", ") AS ?type)
    (GROUP_CONCAT(?topic_label; separator=" // ") AS ?topics)
  WHERE {
    INCLUDE %works
    OPTIONAL {
       ?work wdt:P31 ?type_ .
       ?type_ rdfs:label ?type_label . FILTER (LANG(?type_label) = '{{ g.lang_code }}')
    }
    OPTIONAL { ?work wdt:P577 ?dates . }
    OPTIONAL {
      ?work wdt:P921 ?topic .
      ?topic rdfs:label ?topic_label . FILTER (lang(?topic_label) = '{{ g.lang_code }}')
    }
  }
  GROUP BY ?work
} AS %result
WHERE {
  INCLUDE %result
  BIND(xsd:date(?datetime) AS ?date)
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "{{ g.lang_code }},[AUTO_LANGUAGE],en" .
  }
}
ORDER BY DESC(?date)
LIMIT 500