#defaultView:Table

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# List of related works by co-citation analysis
SELECT ?count ?work ?workLabel
WITH {
  SELECT ?work (COUNT(?work) AS ?count)
  WHERE {
    target: (^wdt:P2860 | wdt:P2860) / (^wdt:P2860 | wdt:P2860)? ?work .
    FILTER (target: != ?work)
  }
  GROUP BY ?work
  ORDER BY DESC(?count)
  LIMIT 500
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . } 
} 
ORDER BY DESC(?count) 
