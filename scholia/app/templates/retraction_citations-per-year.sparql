#defaultView:BarChart
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT (STR(?_year) AS ?year) (COUNT(DISTINCT ?citing_work) AS ?count) ?kind WHERE {
  ?citing_work wdt:P2860 target: ;
               wdt:P577 ?date .
  OPTIONAL { target: p:P31 [ ps:P31 wd:Q45182324 ; pq:P580 ?retractionDate ] }
  # Year of citation
  BIND (YEAR(?date) AS ?_year)
  # Type of citation
  BIND (IF(BOUND(?retractionDate),
    IF(xsd:date(?retractionDate) > xsd:date(?date), "before retraction", "after retraction"),
    "_"
  ) AS ?kind)
} GROUP BY ?_year ?kind
  ORDER BY DESC(?_year)
