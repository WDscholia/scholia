#defaultView:Graph
SELECT
  ?child ?childLabel
  ?rgb 
  ?parent ?parentLabel
WITH {
  SELECT ?child ?rgb ?parent WHERE {
    { 
      # Parent taxa
      SELECT ?child ?rgb ?parent  WHERE {
        SERVICE gas:service {
          gas:program gas:gasClass "com.bigdata.rdf.graph.analytics.BFS" ;
                  gas:in wd:{{ q }} ;
                  # We should not do undirected here because this could result
                  # in very big graphs.
                  gas:traversalDirection "Forward" ;
                  gas:out ?child ;
                  gas:out1 ?depth ;
                  gas:out2 ?parent1 ;
                  gas:linkType wdt:P171 ;
        }
        ?child wdt:P171 ?parent .
        BIND(IF(?child = wd:{{ q }}, "FF0000", "FFFFFF") AS ?rgb)
      }
    }
    UNION 
    {
      # Child taxa
      SELECT ?child ?rgb ?parent WHERE {
        BIND(wd:{{ q }} AS ?parent)
        ?child wdt:P171 ?parent . 
        BIND("DDDDDD" AS ?rgb)
      }
      LIMIT 100
    }
  }
} AS %results
WHERE {
  INCLUDE %results 
          
  ?child rdfs:label ?child_label . FILTER(LANG(?child_label) = 'en')
  ?parent rdfs:label ?parent_label . FILTER(LANG(?parent_label) = 'en')
  OPTIONAL {
    ?child wdt:P105 / rdfs:label ?child_rank_label . FILTER (LANG(?child_rank_label) = 'en')
  }
  OPTIONAL {
    ?parent wdt:P105 / rdfs:label ?parent_rank_label . FILTER (LANG(?parent_rank_label) = 'en')
  }
  BIND(CONCAT(?child_label, " - ", COALESCE(?child_rank_label, "???")) AS ?childLabel)
  BIND(CONCAT(?parent_label, " - ", COALESCE(?parent_rank_label, "???")) AS ?parentLabel)

}
