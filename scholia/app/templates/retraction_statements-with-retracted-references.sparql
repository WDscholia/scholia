PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT DISTINCT ?entity ?entityLabel ?entityUrl
  ?property ?propertyLabel ?value ?valueLabel
WITH {
  SELECT DISTINCT ?entity ?p ?statement ?retracted WHERE {
    BIND(target: AS ?retracted)
    ?entity ?p ?statement .
    ?statement prov:wasDerivedFrom / pr:P248 ?retracted .
  }
} AS %STATEMENTS WHERE {
  INCLUDE %STATEMENTS
  ?p ^wikibase:claim ?property .
  ?statement ?ps ?value .
  ?ps ^wikibase:statementProperty ?property .
  BIND(?statement AS ?entityUrl)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
