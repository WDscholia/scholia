PREFIX target: <http://www.wikidata.org/entity/{{ q }}>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX mwapi: <https://www.mediawiki.org/ontology#API/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT DISTINCT ?item (?title AS ?itemLabel) (CONCAT("/work/", ?work) AS ?itemUrl) ?taxonname ?work ("P921" AS ?main_subject) ?taxon ("S887" AS ?heuristic) ("Q69652283" AS ?deduced) WHERE {
  SERVICE <https://query.wikidata.org/sparql> {
    # this filters for species-level (Q7432) taxon names; comment out or adapt as necessary
    target: wdt:P105 wd:Q7432 .
    target: wdt:P225 ?taxonname .

    SERVICE wikibase:mwapi {
      bd:serviceParam wikibase:endpoint "www.wikidata.org" ;
                      wikibase:api "Generator" ;
                      mwapi:generator "search" ;
                      mwapi:gsrsearch ?taxonname ;
                      mwapi:gsrlimit "max" .
      ?item wikibase:apiOutputItem mwapi:title .
    }
  }

  ?item wdt:P1476 ?title .
  # this filters out works about subspecies of the target
  MINUS { ?item wdt:P921/wdt:P171* target: }

  FILTER (REGEX(?title, CONCAT("\\\\b", ?taxonname, "\\\\b"), "i"))

  BIND (SUBSTR(STR(?item), 32) AS ?work)
  BIND (SUBSTR(STR(target:), 32) AS ?taxon)
} LIMIT 200
