# title: list the isotopes for this chemical element
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT ?isotope (REPLACE(STR(?isotope), ".*/", "") AS ?isotopeLabel) ?protons ?neutrons ?mass ?massUnit (REPLACE(STR(?massUnit), ".*/", "") AS ?massUnitLabel) ?halflife ?halflifeUnit?halflifeUnitLabel
WHERE {
  VALUES ?element { target: }
  ?element wdt:P1086 ?protons .
  ?isotope wdt:P279 ?element ;
           wdt:P1148 ?neutrons ;
           p:P2067 ?massStat ;
           p:P2114 ?halflifeStat .
  ?massStat psv:P2067 ?massValNode .
  ?massValNode wikibase:quantityAmount ?mass ;
               wikibase:quantityUnit ?massUnit .
  ?halflifeStat psv:P2114 ?halflifeValNode .
  ?halflifeValNode wikibase:quantityAmount ?halflife ;
                   wikibase:quantityUnit ?halflifeUnit .
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
} ORDER BY ?neutrons
