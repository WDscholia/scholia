PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work), 32)) AS ?workUrl)
  ?authors ?authorsUrl
  ?topics ?topicsUrl
WITH {
  SELECT 
    ?work
    (GROUP_CONCAT(DISTINCT ?author_label; separator=", ") AS ?authors)
    (CONCAT("../authors/", GROUP_CONCAT(DISTINCT SUBSTR(STR(?author), 32); separator=",")) AS ?authorsUrl)    
    (GROUP_CONCAT(DISTINCT ?topic_label; separator=", ") AS ?topics)
    (CONCAT("../topics/", GROUP_CONCAT(DISTINCT SUBSTR(STR(?topic), 32); separator=",")) AS ?topicsUrl)
  WHERE {
    ?work wdt:P5072 target: .
    OPTIONAL {
      ?work wdt:P50 ?author .
      ?author rdfs:label ?author_label . FILTER(LANG(?author_label) = "en")
    }
    OPTIONAL {
      ?work wdt:P921 ?topic .
      ?topic rdfs:label ?topic_label . FILTER(LANG(?topic_label) = "en")
    }
  }
  GROUP BY ?work
} AS %results
WHERE {
  INCLUDE %results
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
}
