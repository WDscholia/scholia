PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?number_of_citations 
?author ?authorLabel (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
?cited_work_example ?cited_work_exampleLabel (CONCAT("/work/", SUBSTR(STR(?cited_work_example), 32)) AS ?cited_work_exampleUrl)
WITH {
  # Find works about the topic
  SELECT DISTINCT ?work WHERE {
	  ?work wdt:P921 / (wdt:P361+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) ) target: .
  }
} AS %works
WITH {
  # Find cited works
  SELECT (COUNT(?work) AS ?number_of_citations) ?author (SAMPLE(?cited_work) AS ?cited_work_example)
  WHERE {
    INCLUDE %works
    ?cited_work wdt:P50 ?author . 
    ?work wdt:P2860 ?cited_work .
  } 
  GROUP BY ?author
} AS %result
WHERE {
  # Label the results
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . }
} 
ORDER BY DESC(?number_of_citations)
LIMIT 200
