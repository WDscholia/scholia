# title: Authors for specific topics
SELECT
  ((?distinct_topics + ?topics / (?topics + 100)) AS ?score)
  ?author ?authorLabel (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  SELECT
    (COUNT(DISTINCT ?topic) AS ?distinct_topics)
    (COUNT(?topic) AS ?topics)
    ?author
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    VALUES ?topic { {% for q in qs %} wd:{{ q }} {% endfor %} }
    
    # A broad query including "(wdt:P361+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) )" seems to take
    # long time and time out.
    ?work wdt:P921 ?topic  ;
          wdt:P50 ?author . 
  }
  GROUP BY ?author
  ORDER BY DESC(?distinct_topics) DESC(?topics)
  LIMIT 200
} AS %results
WHERE {
  INCLUDE %results
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?score)
