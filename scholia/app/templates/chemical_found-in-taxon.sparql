# title: lists the taxons in which this chemical is found
#sparql_endpoint = https://query.wikidata.org/sparql
#sparql_endpoint_name= WDQS main
#sparql_editurl = https://query.wikidata.org/#
#sparql_embedurl = https://query.wikidata.org/embed.html#
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT DISTINCT ?taxon ?taxonLabel (CONCAT("/taxon/", SUBSTR(STR(?taxon), 32)) AS ?taxonUrl)
?source ?sourceLabel (CONCAT("/work/", SUBSTR(STR(?source), 32)) AS ?sourceUrl)
?doi WHERE {
  VALUES ?chemical { target: }
  ?chemical p:P703 ?taxonStatement .
  ?taxonStatement ps:P703 ?taxon .
  OPTIONAL {
    ?taxonStatement prov:wasDerivedFrom/pr:P248 ?source .
    {
      OPTIONAL { ?source wdt:P356 ?doi . }
    } UNION {
      SERVICE wdsubgraph:scholarly_articles {
        ?source rdfs:label ?sourceLabel . FILTER (lang(?sourceLabel) = "mul" || lang(?sourceLabel) = "en")
        OPTIONAL { ?source wdt:P356 ?doi . }
      }
    }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
} ORDER BY ASC(?taxonLabel)
  LIMIT 250
