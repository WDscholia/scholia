# title: lists the taxons in which this chemical is found
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT DISTINCT ?taxon (REPLACE(STR(?taxon), ".*/", "") AS ?taxonLabel) (CONCAT("/taxon/", SUBSTR(STR(?taxon), 32)) AS ?taxonUrl)
?source ?sourceLabel (CONCAT("/work/", SUBSTR(STR(?source), 32)) AS ?sourceUrl)
?doi WHERE {
  VALUES ?chemical { target: }
  ?chemical p:P703 ?taxonStatement .
  ?taxonStatement ps:P703 ?taxon .
  OPTIONAL {
      ?taxonStatement prov:wasDerivedFrom/pr:P248 ?source .
      OPTIONAL { ?source wdt:P356 ?doi . }
    }
#   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
} ORDER BY ASC(?taxonLabel)
  LIMIT 250
