#defaultView:BarChart

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?year
  (COUNT(?work) AS ?number_of_works)
  ?useLabel
WITH{
  SELECT
    (MIN(?year_) AS ?year) ?work ?use
  WHERE {
    ?use wdt:P279* target: .
    ?work wdt:P4510 ?use ; 
          wdt:P577 ?publication_datetime .
    BIND(STR(YEAR(?publication_datetime)) AS ?year_)
  }
  GROUP BY ?work ?use
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
GROUP BY ?year ?useLabel
ORDER BY ?year
